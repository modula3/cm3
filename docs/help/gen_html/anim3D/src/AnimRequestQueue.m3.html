<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: anim3D/src/AnimRequestQueue.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>anim3D/src/AnimRequestQueue.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Created by Marc Najork                                                    
 Last modified on Mon Jul 18 13:54:17 PDT 1994 by najork                   

<P>
<P><PRE>MODULE <module>AnimRequestQueue</module> EXPORTS <A HREF="AnimRequestQueue.i3.html"><implements>AnimRequestQueue</A></implements>, <A HREF="AnimRequestQueuePrivate.i3.html"><implements>AnimRequestQueuePrivate</A></implements>;

IMPORT <A HREF="AnimHandle.i3.html">AnimHandle</A>, <A HREF="AnimHandlePrivate.i3.html">AnimHandlePrivate</A>, <A HREF="Prop.i3.html">Prop</A>;

REVEAL
  <A NAME="T">T</A> = Private BRANDED OBJECT
  OVERRIDES
    init     := Init;
    insert   := Insert;
    duration := Duration;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (self : T; ah : AnimHandle.T) : T =
  BEGIN
    self.list := NIL;
    self.ah := ah;
    ah.attach (self);
    RETURN self;
  END Init;

PROCEDURE <A NAME="Insert"><procedure>Insert</procedure></A> (self : T; req : Prop.Request) RAISES {Prop.BadInterval} =

  PROCEDURE InsertIntoList (VAR l : List) RAISES {Prop.BadInterval} =
    BEGIN
      IF l = NIL THEN
        l := NEW (List, req := req, next := NIL);
      ELSIF Before (req.start, req.dur, l.req.start, l.req.dur) THEN
        l := NEW (List, req := req, next := l);
      ELSE
        InsertIntoList (l.next);
      END;
    END InsertIntoList;

  BEGIN
    LOCK self.ah.mu DO
      InsertIntoList (self.list);
    END;
  END Insert;
</PRE> Returns TRUE if interval1 is before interval2,
           FALSE if interval1 is after interval2,
   raises Prop.BadInterval if they overlap. 
<PRE>PROCEDURE <A NAME="Before"><procedure>Before</procedure></A> (start1, dur1, start2, dur2 : REAL) : BOOLEAN
    RAISES {Prop.BadInterval} =
  BEGIN
    IF dur1 = 0.0 AND dur2 = 0.0 THEN
      IF    start1 &lt; start2 THEN
        RETURN TRUE;
      ELSIF start1 &gt; start2 THEN
        RETURN FALSE;
      ELSE
        RAISE Prop.BadInterval;
      END;
    ELSE
      IF start1 + dur1 &lt;= start2 THEN
        RETURN TRUE;
      ELSIF start2 + dur2 &lt;= start1 THEN
        RETURN FALSE;
      ELSE
        RAISE Prop.BadInterval;
      END;
    END;
  END Before;

PROCEDURE <A NAME="Duration"><procedure>Duration</procedure></A> (self : T) : REAL =
  VAR
    l   : List;
    max := 0.0;
  BEGIN
    l := self.list;
    WHILE l # NIL DO
      max := MAX (max, l.req.start + l.req.dur);
      l := l.next;
    END;
    RETURN max;
  END Duration;

BEGIN
END AnimRequestQueue.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
