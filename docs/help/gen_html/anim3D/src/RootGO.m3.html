<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: anim3D/src/RootGO.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>anim3D/src/RootGO.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation                         
 Digital Internal Use Only                                                 
 All rights reserved.                                                      
                                                                           
 Last modified on Mon Oct  9 13:34:22 PDT 1995 by najork                   
       Created on Wed Feb 16 14:15:37 PST 1994 by najork                   

<P>
<P><PRE>MODULE <module>RootGO</module> EXPORTS <A HREF="RootGO.i3.html"><implements>RootGO</A></implements>, <A HREF="RootGOPrivate.i3.html"><implements>RootGOPrivate</A></implements>, <A HREF="RootGOProxy.i3.html"><implements>RootGOProxy</A></implements>;

IMPORT <A HREF="AmbientLightGO.i3.html">AmbientLightGO</A>, <A HREF="AnimServer.i3.html">AnimServer</A>, <A HREF="BooleanProp.i3.html">BooleanProp</A>, <A HREF="BooleanPropPrivate.i3.html">BooleanPropPrivate</A>, <A HREF="CameraGO.i3.html">CameraGO</A>,
       <A HREF="CameraGOPrivate.i3.html">CameraGOPrivate</A>, <A HREF="../../vbtkit/src/color/Color.i3.html">Color</A>, <A HREF="ColorProp.i3.html">ColorProp</A>, <A HREF="ColorPropPrivate.i3.html">ColorPropPrivate</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="GO.i3.html">GO</A>, <A HREF="GOPrivate.i3.html">GOPrivate</A>,
       <A HREF="GraphicsBase.i3.html">GraphicsBase</A>, <A HREF="GraphicsBasePrivate.i3.html">GraphicsBasePrivate</A>, <A HREF="GroupGO.i3.html">GroupGO</A>, <A HREF="GroupGOPrivate.i3.html">GroupGOPrivate</A>, <A HREF="MouseCB.i3.html">MouseCB</A>,
       <A HREF="../../parseparams/src/ParseParams.i3.html">ParseParams</A>, <A HREF="PerspCameraGO.i3.html">PerspCameraGO</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="Point3.i3.html">Point3</A>, <A HREF="PositionCB.i3.html">PositionCB</A>, <A HREF="Prop.i3.html">Prop</A>, <A HREF="RealProp.i3.html">RealProp</A>,
       <A HREF="RealPropPrivate.i3.html">RealPropPrivate</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="TransformProp.i3.html">TransformProp</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="VectorLightGO.i3.html">VectorLightGO</A>,
       <A HREF="Win_OpenGL_Base.i3.html">Win_OpenGL_Base</A>, <A HREF="X_OpenGL_Base.i3.html">X_OpenGL_Base</A>, <A HREF="X_PEX_Base.i3.html">X_PEX_Base</A>;

REVEAL
  <A NAME="T">T</A> = Private BRANDED OBJECT
    backgroundColor : Color.T;  (* cached background color *)
  OVERRIDES
    init                := Init;
    changeCamera        := ChangeCamera;
    screenToWorld       := ScreenToWorld;
    findName            := FindName;
    adjust              := Adjust;
    draw                := Draw;
    damageIfDependent   := DamageIfDependent;
    undamage            := Undamage;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (self : T; cam : CameraGO.T; base : GraphicsBase.T) : T =
  BEGIN
    EVAL GroupGO.T.init (self);
    self.cam   := cam;
    self.base  := base;

    base.root := self;

    (* set the background color to a sentinel value, thereby triggering
       an initial damage repair, i.e. proper background initialization ***)
    self.backgroundColor := Color.T {-1.0, -1.0, -1.0};

    (* Register to root with the animation server *)
    AnimServer.RegisterRootGO (self);

    IF MkProxyT # NIL THEN
      MkProxyT (self);
    END;

    RETURN self;
  END Init;

PROCEDURE <A NAME="ChangeCamera"><procedure>ChangeCamera</procedure></A> (self : T; cam : CameraGO.T) =
  BEGIN
    (*** Must be protected from interference with the animation server ***)
    LOCK AnimServer.internalLock DO
      self.cam := cam;

      (*** damage the root, forcing a redraw ***)
      self.damaged := TRUE;
    END;
  END ChangeCamera;

PROCEDURE <A NAME="ScreenToWorld"><procedure>ScreenToWorld</procedure></A> (self: T; pos: Point.T; z: REAL): Point3.T =
  BEGIN
    RETURN self.base.screenToWorld (pos, z);
  END ScreenToWorld;

PROCEDURE <A NAME="FindName"><procedure>FindName</procedure></A> (self : T; name : TEXT) : GO.T =
  BEGIN
    IF self.cam.findName (name) # NIL THEN
      RETURN self.cam;
    ELSE
      RETURN GroupGO.T.findName (self, name);
    END;
  END FindName;

PROCEDURE <A NAME="Adjust"><procedure>Adjust</procedure></A> (self : T; time : LONGREAL) =
  BEGIN
    (*** Adjust self like any other root ... ***)
    GroupGO.T.adjust (self, time);

    (*** ... but also adjust the active camera ... ***)
    self.cam.adjust (time);

    (*** ... and propagate its damage up. ***)
    IF self.cam.damaged THEN
      self.damaged := TRUE;
    END;
  END Adjust;

PROCEDURE <A NAME="Draw"><procedure>Draw</procedure></A> (self : T; state : GraphicsBase.T) =
  BEGIN
    state.push (self);

    (*** Take care of the background color ***)
    WITH col = Background.getState (state) DO
      IF self.backgroundColor # col THEN
        self.backgroundColor := col;
        state.setBackgroundColor (col);
      END;
    END;

    (*** Take care of depth cueing ***)
    WITH switch = DepthcueSwitch.getState (state),
         fplane = DepthcueFrontPlane.getState (state),
         bplane = DepthcueBackPlane.getState (state),
         fscale = DepthcueFrontScale.getState (state),
         bscale = DepthcueBackScale.getState (state),
         color  = DepthcueColour.getState (state) DO
      state.setDepthcueing(switch, fplane, bplane, fscale, bscale, color);
    END;

    (*** Now do whatever has to be done for normal groups as well ***)
    FOR i := 0 TO self.last DO
      (* Calling draw may set self.damaged *)
      self.children[i].draw (state);
    END;

    state.pop (self);

    (* Test if the camera has been drawn (i.e. is part of the overall scene);
       if this is not the case, draw it now. *)
    IF NOT self.cam.flag THEN
      (* Note that the order of the arguments to OR matters here!! *)
      self.cam.draw (state);
    END;
    self.cam.flag := FALSE;

    (*** As &quot;caller&quot; is NIL, we don't have to propagate self.damaged ***)
  END Draw;

PROCEDURE <A NAME="DamageIfDependent"><procedure>DamageIfDependent</procedure></A> (self : T; pn : Prop.Name) =
  BEGIN
    IF pn = Background OR pn = DepthcueSwitch OR pn = DepthcueColour OR
       pn = DepthcueFrontPlane OR pn = DepthcueBackPlane OR
       pn = DepthcueFrontScale OR pn = DepthcueBackScale THEN
      self.damaged := TRUE;
    END;
  END DamageIfDependent;

PROCEDURE <A NAME="Undamage"><procedure>Undamage</procedure></A> (self: T) =
  BEGIN
    GroupGO.T.undamage (self);
    self.cam.undamage ();
  END Undamage;
</PRE>***************************************************************************
 Construction procedures                                                   
***************************************************************************

<P>
<P><PRE>PROCEDURE <A NAME="New"><procedure>New</procedure></A> (cam : CameraGO.T; base : GraphicsBase.T) : T =
  BEGIN
    RETURN NEW(T).init(cam, base);
  END New;

PROCEDURE <A NAME="NewStd"><procedure>NewStd</procedure></A> (base : GraphicsBase.T) : T RAISES {GraphicsBase.Failure} =

  PROCEDURE NewBase (title: TEXT) : GraphicsBase.T
      RAISES {GraphicsBase.Failure} =
    TYPE
      Pref = {None, X_PEX, X_OpenGL};
    VAR
      pref := Pref.None;
    BEGIN
      (*** Check for environment variable $prefPref ***)
      WITH var = Env.Get (&quot;prefBase&quot;) DO
        IF var # NIL THEN
          IF Text.Equal (var, &quot;X_PEX&quot;) THEN
            pref := Pref.X_PEX;
          ELSIF Text.Equal (var, &quot;X_OpenGL&quot;) THEN
            pref := Pref.X_OpenGL;
          END;
        END;
      END;

      (*** Command-line arguments &quot;-prefBase&quot; overrides $prefBase ***)
      WITH pp = NEW (ParseParams.T).init (Stdio.stderr) DO
        IF pp.keywordPresent(&quot;-prefBase&quot;) THEN
          TRY
            WITH val = pp.getNext () DO
              IF Text.Equal (val, &quot;X_PEX&quot;) THEN
                pref := Pref.X_PEX;
              ELSIF Text.Equal (val, &quot;X_OpenGL&quot;) THEN
                pref := Pref.X_OpenGL;
              END;
            END;
          EXCEPT
            ParseParams.Error =&gt;  (* ignore ... *)
          END;
        END;
      END;

      (* Try to create the preferred base.
         If this does not succeed, create any base. *)
      TRY
        CASE pref OF
        | Pref.None     =&gt;
        | Pref.X_PEX    =&gt; RETURN NEW (X_PEX_Base.T).init (title);
        | Pref.X_OpenGL =&gt; RETURN NEW (X_OpenGL_Base.T).init (title);
        END;
      EXCEPT
        GraphicsBase.Failure =&gt;
      END;

      TRY
        RETURN NEW (X_PEX_Base.T).init (title);
      EXCEPT
        GraphicsBase.Failure =&gt;
      END;
      TRY
        RETURN NEW (X_OpenGL_Base.T).init (title);
      EXCEPT
        GraphicsBase.Failure =&gt;
      END;
      TRY
        RETURN NEW (Win_OpenGL_Base.T).init (title);
      EXCEPT
        GraphicsBase.Failure =&gt;
      END;

      RAISE GraphicsBase.Failure;
    END NewBase;

  VAR
    root : T;
    cam := PerspCameraGO.New (from := Point3.T{0.0, 0.0, 100.0},
                              to   := Point3.T{0.0, 0.0, 0.0},
                              up   := Point3.T{0.0, 1.0, 0.0},
                              fovy := 0.05);
  BEGIN
    IF base = NIL THEN
      base := NewBase (&quot;Anim3D Viewer&quot;);
    END;
    cam.setName (&quot;default-camera&quot;);
    root := NEW (T).init (cam, base);

    (* Attach two lights *)
    WITH light = AmbientLightGO.New (Color.White) DO
      light.setName (&quot;default-ambient-light&quot;);
      root.add (light);
    END;
    WITH light = VectorLightGO.New (Color.White, Point3.T{-1.0,-1.0,-1.0}) DO
      light.setName (&quot;default-vector-light&quot;);
      root.add (light);
    END;

    (* Attach mouse and position callbacks to the root *)
    root.setProp (GO.Transform.bind (TransformProp.NewConst ()));
    root.pushMouseCB (NEW (MyMouseCB, go := root, invoke := MouseInvoke).init());

    RETURN root;
  END NewStd;

TYPE
  MyPositionCB = PositionCB.T OBJECT
    go  : T;
    pos : Point.T;
    but : VBT.Button;
  OVERRIDES
    invoke := PositionInvoke;
  END;

  MyMouseCB = MouseCB.T OBJECT
    go : T;
  OVERRIDES
    invoke := MouseInvoke;
  END;

PROCEDURE <A NAME="PositionInvoke"><procedure>PositionInvoke</procedure></A> (self : MyPositionCB; pr : PositionCB.Rec) =
  &lt;* FATAL GO.PropUndefined *&gt;
  BEGIN
    WITH d   = Point.Sub (pr.pos2D, self.pos),
         dx  = FLOAT (d.h), dy = FLOAT (d.v),
         beh = NARROW (GO.GetTransform(self.go).beh, TransformProp.ConstBeh) DO
      IF VBT.Modifier.Shift IN pr.modifiers THEN
        CASE  self.but OF
        | VBT.Modifier.MouseL =&gt; beh.translate (dx * 0.01, -dy * 0.01, 0.0);
        | VBT.Modifier.MouseM =&gt; beh.scale (1.0 + dx * 0.01,
                                            1.0 + dx * 0.01,
                                            1.0 + dx * 0.01);
        | VBT.Modifier.MouseR =&gt; beh.translate (0.0, 0.0, dx * 0.01);
        ELSE
          (* Mice have only three buttons those days ... *)
        END;
      ELSE
        CASE  self.but OF
        | VBT.Modifier.MouseL =&gt; beh.rotateX (dx * 0.01);
        | VBT.Modifier.MouseM =&gt; beh.rotateY (dx * 0.01);
        | VBT.Modifier.MouseR =&gt; beh.rotateZ (dx * 0.01);
        ELSE
          (* Mice have only three buttons those days ... *)
        END;
      END;
    END;
    self.pos := pr.pos2D;
  END PositionInvoke;

PROCEDURE <A NAME="MouseInvoke"><procedure>MouseInvoke</procedure></A> (self : MyMouseCB; mr : MouseCB.Rec) =
  &lt;* FATAL GO.StackError *&gt;
  BEGIN
    IF mr.clickType = VBT.ClickType.FirstDown THEN
      self.go.pushPositionCB (NEW (MyPositionCB,
                                   go  := self.go,
                                   pos := mr.pos2D,
                                   but := mr.whatChanged).init());
    ELSIF mr.clickType = VBT.ClickType.LastUp THEN
      self.go.popPositionCB ();
    END;
  END MouseInvoke;
</PRE>***************************************************************************
 Module body                                                               
***************************************************************************

<P>
<P><PRE>BEGIN
  Background         := NEW (ColorProp.Name).init (Color.Black);
  DepthcueColour     := NEW (ColorProp.Name).init (Color.Black);
  DepthcueFrontPlane := NEW (RealProp.Name).init (1.0);
  DepthcueBackPlane  := NEW (RealProp.Name).init (0.0);
  DepthcueFrontScale := NEW (RealProp.Name).init (1.0);
  DepthcueBackScale  := NEW (RealProp.Name).init (0.0);
  DepthcueSwitch     := NEW (BooleanProp.Name).init (FALSE);
END RootGO.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
