<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: anim3D/src/GraphicsBasePrivate.i3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>anim3D/src/GraphicsBasePrivate.i3</H2></A><HR>
<inInterface>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation                         
 Digital Internal Use Only                                                 
 All rights reserved.                                                      
                                                                           
 Last modified on Fri Jul 14 17:20:02 PDT 1995 by najork                   
       Created on Wed Feb 16 17:57:52 PST 1994 by najork                   

<P>
<P><PRE>INTERFACE <interface><A HREF="GraphicsBase.m3.html">GraphicsBasePrivate</A></interface>;

IMPORT <A HREF="BSphere.i3.html">BSphere</A>, <A HREF="../../vbtkit/src/color/Color.i3.html">Color</A>, <A HREF="GO.i3.html">GO</A>, <A HREF="LineTypeProp.i3.html">LineTypeProp</A>, <A HREF="MarkerTypeProp.i3.html">MarkerTypeProp</A>, <A HREF="Matrix4.i3.html">Matrix4</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>,
       <A HREF="Point3.i3.html">Point3</A>, <A HREF="PropPrivate.i3.html">PropPrivate</A>, <A HREF="ProxiedObj.i3.html">ProxiedObj</A>, <A HREF="RasterModeProp.i3.html">RasterModeProp</A>, <A HREF="RootGO.i3.html">RootGO</A>, <A HREF="ShadingProp.i3.html">ShadingProp</A>;

FROM <A HREF="GraphicsBase.i3.html">GraphicsBase</A> IMPORT T;

CONST
  VoidColor = Color.T {-1.0, -1.0, -1.0};

REVEAL T &lt;: Private;

TYPE
  Private = ProxiedObj.T BRANDED OBJECT
    root   : RootGO.T := NIL;
    status : Status   := Status.Unmapped;
    stacks : REF ARRAY OF PropPrivate.Stack;
  METHODS
    unmap ();
    processEvents ();
    repair (VAR damaged : BOOLEAN);

    (*** Pushing and popping property lists onto the state **)
    push (caller : GO.T);
    pop (caller : GO.T);

    (*** Display Lists ***)
    openDisplayList (go : GO.T);
    closeDisplayList ();
    callDisplayList (go : GO.T);
    freeDisplayList (go : GO.T);

    (*** The matrix stack ***)
    pushMatrix (READONLY matrix : Matrix4.T);
    popMatrix ();

    (* Bounding volumes *)
    resetBoundingVolume ();
    growBoundingVolume (center : Point3.T; radius : REAL);
    getBoundingVolume () : BSphere.T;

    (* Camera control - setting viewing and projection transformations, and
       mapping points from screen coordinate space to world coordinate space *)
    setLookAt (from, to, up: Point3.T);
    setPerspProj (fovy, aspect: REAL);
    setOrthoProj (height, aspect: REAL);
    setupCamera ();
    screenToWorld (pos: Point.T; z: REAL): Point3.T;

    (*** Graphics Commands ***)
    setBackgroundColor (color : Color.T);
    setDepthcueing (switch: BOOLEAN;
                    frontPlane, backPlane, frontScale, backScale: REAL;
                    color: Color.T);

    setMarkerColor (val : Color.T);
    setMarkerScale (val : REAL);
    setMarkerType  (val : MarkerTypeProp.Kind);

    setLineColor (val : Color.T);
    setLineWidth (val : REAL);
    setLineType  (val : LineTypeProp.Kind);

    setSurfaceColor (val : Color.T);
    setSurfaceBackColor (val : Color.T);
       (* setting the back color to &quot;VoidColor&quot; indicates that it shall
          be the same as the front color *)
    setRasterMode (val : RasterModeProp.Kind);
    setDistinguishFacetsFlag (val : BOOLEAN);
    setLighting (val : BOOLEAN);
    setShading (val : ShadingProp.Kind);
    setSurfaceEdgeFlag (val : BOOLEAN);
    setSurfaceEdgeColor (val : Color.T);
    setSurfaceEdgeType (val : LineTypeProp.Kind);
    setSurfaceEdgeWidth (val : REAL);

    setAmbientReflCoeff (val : REAL);
    setDiffuseReflCoeff (val : REAL);
    setSpecularReflCoeff (val : REAL);
    setSpecularReflConc (val : REAL);
    setSpecularReflColor (val : Color.T);
    setTransmissionCoeff (val : REAL);

    addAmbientLight (color: Color.T);
    addVectorLight  (color: Color.T; dir: Point3.T);
    addPointLight (color: Color.T; point: Point3.T; att0, att1: REAL);
    addSpotLight (color: Color.T; point, dir: Point3.T;
                  conc, spread, att0, att1: REAL);

    drawMarker (p : Point3.T);
    drawLine (p1, p2 : Point3.T);
    drawPolygon (READONLY pts : ARRAY OF Point3.T; shape : GO.Shape);
    drawQuadMesh (READONLY pts : ARRAY OF ARRAY OF Point3.T; shape : GO.Shape);
    drawColoredQuadMesh (READONLY points: ARRAY OF ARRAY OF Point3.T;
                         READONLY colors: ARRAY OF ARRAY OF Color.T;
                                  shape : GO.Shape);

    drawProtoSphere (prec : INTEGER);
    drawProtoCone (prec : INTEGER);
    drawProtoCylinder (prec : INTEGER);
    drawProtoDisk (prec : INTEGER);
    drawProtoTorus (prec: INTEGER; radiusRatio : REAL);
  END;
</PRE> <CODE>base.repair(damaged)</CODE> redraws the scene rooted at <CODE>base.root</CODE>. Only those
   nodes that were marked as damaged are re-rendered, for the other nodes,
   cached values are used.
<P>
   Nodes can be damaged in two ways:
   \begin{enumerate}
   \item By operations that change the scene DAG (i.e.\ the group operations
         <CODE>add</CODE>, <CODE>remove</CODE>, and <CODE>flush</CODE>).
   \item By changes in the value of an attached property.
   \end{enumerate}
<P>
   Calling <CODE>root.adjust(time)</CODE> serves two purposes: It reevaluates all the
   properties attached to all descendants of <CODE>root</CODE>, and damages those nodes
   whose properties have changed since the last round of rendering. It also
   propagates damages up the scene DAGs.
       <CODE>
   </CODE>base.repair(damaged)<CODE> is called after all roots have been adjusted. The
   VAR parameter </CODE>damaged<CODE> is set to TRUE if there were any damages in the
   scene, otherwise, it remains unchanged. 

<P><PRE>TYPE Status = {Unmapped, Mapped, Destroyed};

END GraphicsBasePrivate.
</PRE>
</inInterface>
<PRE>























</PRE>
</BODY>
</HTML>
