<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: jvideo/src/POSIX/Jvs.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>jvideo/src/POSIX/Jvs.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1989, 1990, Digital Equipment Corporation 
 All rights reserved. 
 See the file COPYRIGHT for a full description. 

<P> Last modified on Wed Mar 22 18:17:10 PST 1995 by msm      
      modified on Tue Feb 22 23:09:30 PST 1994 by kalsow   
      modified on Mon Oct 25 12:19:23 PDT 1993 by sfreeman 

<P> note: SIGPIPE set to be ignored by JVSink 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="Jvs.i3.html">Jvs</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="../../../m3core/src/C/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="Jv.i3.html">Jv</A>, <A HREF="JvsProtocol.i3.html">JvsProtocol</A>, <A HREF="../../../m3core/src/C/Common/M3toC.i3.html">M3toC</A>, <A HREF="../../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../../libm3/src/geometry/Point.i3.html">Point</A>,
       <A HREF="../../../m3core/src/runtime/common/RTMisc.i3.html">RTMisc</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../m3core/src/word/Word.i3.html">Word</A>;

CONST DefaultColors = 50;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        dparams                 := DefaultDecompress;
        cmap   : ColormapInfo;
      OVERRIDES
        init             := Init;
        allocateBuffer   := AllocateBuffer;
        deallocateBuffer := DeallocateBuffer;
        compress         := Compress;
        setCompress      := SetCompress;
        decompress       := Decompress;
        setDecompress    := SetDecompress;
        colormap         := Colormap;
        close            := Close;
      END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (t: T): T RAISES {OSError.E} =
  BEGIN
    TRY
      LOCK t DO RETURN Jv.T.init(t, JvsProtocol.PipeName); END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
  END Init;

PROCEDURE <A NAME="AllocateBuffer"><procedure>AllocateBuffer</procedure></A> (t: T; type: BufferType; width, height: CARDINAL):
  ShmBufId RAISES {OSError.E, Thread.Alerted} =
  VAR
    req: JvsProtocol.AllocateReq;
    rep: JvsProtocol.AllocateRep;
  BEGIN
    req.requestCode := JvsProtocol.Allocate;
    TRY
      CASE type OF
      | BufferType.Compress =&gt;
          IF width # 0 THEN
            req.requestCode := JvsProtocol.Allocate2
          END;
          req.type := JvsProtocol.JPEG;
          req.width := width;
          req.height := 0;
      | BufferType.Decompress =&gt;
          req.type := JvsProtocol.Dithered;
          IF width = 0 AND height = 0 THEN
            req.width := 1280;
            req.height := 1024
          ELSE
            req.width := width;
            req.height := height
          END
      END;
      req.direction := JvsProtocol.Output;
      LOCK t DO
        Jv.Send(t, ADR(req), BYTESIZE(req));
        Jv.Recv(t, ADR(rep), BYTESIZE(rep));
      END;
      IF rep.requestCode # JvsProtocol.Allocate THEN
        (* error code of some sort *)
        RETURN 0;
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
    RETURN rep.shmid;
  END AllocateBuffer;

PROCEDURE <A NAME="DeallocateBuffer"><procedure>DeallocateBuffer</procedure></A> (t: T; shmid: ShmBufId)
  RAISES {OSError.E, Thread.Alerted} =
  VAR
    req: JvsProtocol.DeallocateReq;
    rep: JvsProtocol.DeallocateRep;
  BEGIN
    TRY
      req.shmid := shmid;
      LOCK t DO
        Jv.Send(t, ADR(req), BYTESIZE(req));
        Jv.Recv(t, ADR(rep), BYTESIZE(rep));
      END;
      IF rep.requestCode # req.requestCode THEN
        RAISE OSError.E(AtomList.List1(Atom.FromText(&quot;DeallocateBuffer&quot;)));
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
  END DeallocateBuffer;

PROCEDURE <A NAME="Compress"><procedure>Compress</procedure></A> (t: T; dest: ShmBufId): CARDINAL
  RAISES {OSError.E, Thread.Alerted} =
  VAR
    req: JvsProtocol.CompressReq;
    rep: JvsProtocol.CompressRep;
  BEGIN
    TRY
      req.shmid := dest;
      LOCK t DO
        Jv.Send(t, ADR(req), BYTESIZE(req));
        Jv.Recv(t, ADR(rep), BYTESIZE(rep));
      END;
      IF rep.requestCode # req.requestCode OR rep.shmid # req.shmid THEN
        RAISE OSError.E(AtomList.List1(Atom.FromText(&quot;Compress&quot;)));
      END;
      RETURN rep.length;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
  END Compress;

PROCEDURE <A NAME="SetCompress"><procedure>SetCompress</procedure></A> (t: T; qfactor, xdec, ydec, frameskip: INTEGER):
  Point.T RAISES {OSError.E, Thread.Alerted} =
  VAR
    req: JvsProtocol.SetCompressReq;
    rep: JvsProtocol.SetCompressRep;
  BEGIN
    TRY
      req.qfactor := qfactor;
      req.xdec := xdec;
      req.ydec := ydec;
      req.frameskip := frameskip;
      LOCK t DO
        Jv.Send(t, ADR(req), BYTESIZE(req));
        Jv.Recv(t, ADR(rep), BYTESIZE(rep));
      END;
      IF rep.requestCode # req.requestCode THEN
        RAISE OSError.E(AtomList.List1(Atom.FromText(&quot;SetCompress&quot;)));
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
    RETURN Point.T{rep.width, rep.height};
  END SetCompress;

PROCEDURE <A NAME="Decompress"><procedure>Decompress</procedure></A> (t: T; src, dest: ShmBufId; srcByteLength: CARDINAL)
  RAISES {OSError.E, Thread.Alerted} =
  VAR
    req: JvsProtocol.DecompressReq;
    rep: JvsProtocol.DecompressRep;
  BEGIN
    TRY
      req.cshmid := src;
      req.dshmid := dest;
      req.length := srcByteLength;
      LOCK t DO
        Jv.Send(t, ADR(req), BYTESIZE(req));
        Jv.Recv(t, ADR(rep), BYTESIZE(rep));
      END;
      IF rep.requestCode # req.requestCode OR rep.cshmid # req.cshmid
           OR rep.dshmid # req.dshmid THEN
        RAISE OSError.E(AtomList.List1(DecompressFailure));
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
  END Decompress;
</PRE> the stuff for aligning the X axis is taken from Lance's JVideo.c code.
   He can explain it to you... 
<PRE>CONST
  DesiredRounding = BYTESIZE(Word.T);

PROCEDURE <A NAME="SetDecompress"><procedure>SetDecompress</procedure></A> (t: T; VAR params: DcmpParams): BOOLEAN
  RAISES {OSError.E, Thread.Alerted} =
  VAR
    req         : JvsProtocol.SetDecompressReq;
    rep         : JvsProtocol.SetDecompressRep;
    roundedWidth: CARDINAL;
  BEGIN
    TRY
      LOCK t DO
        IF params = t.dparams THEN RETURN FALSE; END;

        req.qfactor := params.qfactor;
        req.inX := params.inX;
        req.inY := params.inY;
        req.outX := params.reqX;
        req.outY := params.reqY;
        req.brightness := params.brightness;
        req.contrast := params.contrast;
        req.saturation := params.saturation;

        LOOP
          Jv.Send(t, ADR(req), BYTESIZE(req));
          Jv.Recv(t, ADR(rep), BYTESIZE(rep));
          IF rep.requestCode # req.requestCode THEN
            RAISE
              OSError.E(AtomList.List1(Atom.FromText(&quot;SetDecompress&quot;)));
          END;
          roundedWidth := (((rep.actualOutX + rep.linePadding + 3) DIV 4) * 4);
          IF (roundedWidth MOD DesiredRounding) = 0 THEN EXIT; END;
          DEC(req.outX);
        END;

	linePadding := rep.linePadding;
        params.outX := roundedWidth;
        params.outY := rep.actualOutY;
        t.dparams := params;
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
    RETURN TRUE;
  END SetDecompress;

CONST BoolToInt = ARRAY BOOLEAN OF Ctypes.int{0, 1};

PROCEDURE <A NAME="Colormap"><procedure>Colormap</procedure></A> (t: T; VAR info: ColormapInfo): BOOLEAN
  RAISES {OSError.E, Thread.Alerted} =
  VAR
    req   : JvsProtocol.ColormapReq;
    rep   : JvsProtocol.ColormapRep;
    length: CARDINAL;
  BEGIN
    TRY
      LOCK t DO
        WITH cmap = t.cmap DO
          IF cmap.id = info.id AND
               ((info.nColors = 0) # (cmap.nColors = info.nColors))
               AND cmap.monochrome = info.monochrome THEN
            RETURN FALSE;
          END;

          IF info.displayName # NIL THEN
            length := Text.Length(info.displayName);
            IF length &gt; JvsProtocol.MaxXServerNameLen - 1 THEN
              RAISE OSError.E(AtomList.List1(XNameTooLong));
            END;

            WITH string = M3toC.SharedTtoS(info.displayName) DO
              RTMisc.Copy(string, ADR(req.serverName[0]), length + 1);
              M3toC.FreeSharedS(info.displayName,string);
            END;
          ELSE
            RTMisc.Zero(ADR(req.serverName[0]), BYTESIZE(req.serverName));
          END;

          req.nColors := info.nColors;
          req.id := info.id;
          req.monochrome := BoolToInt[info.monochrome];

          LOOP
            Jv.Send(t, ADR(req), BYTESIZE(req));
            Jv.Recv(t, ADR(rep), BYTESIZE(rep));
            IF rep.requestCode # req.requestCode THEN
              RAISE OSError.E(AtomList.List1(Atom.FromText(&quot;Colormap&quot;)));
            END;
            IF rep.nColors # 0 OR req.nColors # 0 THEN EXIT END;
            req.nColors := DefaultColors
          END;

          info.nColors := rep.nColors;
          t.cmap := info;
        END;
      END;
    EXCEPT
    | OSError.E (e) =&gt; RAISE OSError.E(AtomList.Cons(Jv.ServerFailure, e));
    END;
    RETURN TRUE;
  END Colormap;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A> (t: T) =
  BEGIN
    LOCK t DO Jv.T.close(t); END;
  END Close;

BEGIN
  XNameTooLong := Atom.FromText(&quot;Jvs XNameTooLong&quot;);
  DecompressFailure := Atom.FromText(&quot;Decompress&quot;);
END Jvs.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
