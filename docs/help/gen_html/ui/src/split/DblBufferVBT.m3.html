<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: ui/src/split/DblBufferVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>ui/src/split/DblBufferVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Jan 15 13:09:17 PST 1996 by heydon                   
      modified on Fri Jul  8 17:10:10 PDT 1994 by msm                      
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="DblBufferVBT.i3.html">DblBufferVBT</A></implements></module>;

IMPORT <A HREF="Filter.i3.html">Filter</A>, <A HREF="FilterClass.i3.html">FilterClass</A>, <A HREF="../vbt/VBTClass.i3.html">VBTClass</A>, <A HREF="../vbt/VBT.i3.html">VBT</A>, <A HREF="../vbt/VBTRep.i3.html">VBTRep</A>, <A HREF="../../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../../libm3/src/geometry/Region.i3.html">Region</A>,
  <A HREF="../vbt/Trestle.i3.html">Trestle</A>, <A HREF="../vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../trestle/InstalledVBT.i3.html">InstalledVBT</A>, <A HREF="../vbt/Batch.i3.html">Batch</A>, <A HREF="../vbt/BatchUtil.i3.html">BatchUtil</A>, <A HREF="../vbt/ScrnPixmap.i3.html">ScrnPixmap</A>,
  <A HREF="../vbt/MouseSplit.i3.html">MouseSplit</A>, <A HREF="DblBufferUtil.i3.html">DblBufferUtil</A>, <A HREF="../vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="HighlightVBT.i3.html">HighlightVBT</A>;
</PRE> A <CODE>DblBufferVBT.T</CODE> <CODE>v</CODE> is implemented by creating a VBT <CODE>offscreen(v)</CODE> that
   is installed off-screen. The <CODE>paintbatch</CODE> method is overridden to forward
   paint batches to <CODE>offscreen(v)</CODE>. The operation <CODE>VBT.Sync(v)</CODE> updates the
   on-screen VBT from the portion of <CODE>offscreen(v)</CODE> that has changed since the
   last update.
<P>
   Because <CODE>offscreen(v)</CODE> is installed off screen, the northwest corner of its
   domain has the coordinates <CODE>(0, 0)</CODE>. The <CODE>reshape</CODE> method arranges for the
   double-buffer's child's domain to agree with the domain of <CODE>offscreen(v)</CODE>.
<P>
   The double-buffer maintains a vector <CODE>delta(v)</CODE>, which is the difference
   between the parent's and child's coordinate systems. In practice, <CODE>delta(v)
   = Rect.Northwest(VBT.Domain(v))</CODE>.
<P>
   The double-buffer maintains two rectangles <CODE>screenDiff(v)</CODE> and
   <CODE>savedDiff(v)</CODE> that satisfy the following invariants:
<P>
<PRE>
         I1: (A p: offscreen(v)(p) # screen(v)(p) =&gt; p IN screenDiff(v))
         I2: (A p: offscreen(v)(p) # savedBuff(v)(p) =&gt; p IN savedDiff(v))
</PRE>
   savedBuff(v) = NIL represents the state where the saved buffer is all
   background.
<P>
   If either <CODE>offScreen(v)</CODE> or <CODE>savedBuff(v)</CODE> are non-NIL, their domains are
   congruent to <CODE>v</CODE>'s domain.


<P><PRE>REVEAL
  <A NAME="T">T</A> = Filter.T BRANDED OBJECT
    &lt;* LL &gt;= { VBT.mu.SELF, SELF } *&gt;
    delta := Point.Origin;          (* child coord + delta = parent coord. *)
    screenId: VBT.ScreenID := -1;
    &lt;* LL &gt;= { SELF } *&gt;
    offScreen, savedBuff: VBT.T := NIL;
    screenDiff, savedDiff: Rect.T;            (* both in child coordinates *)
  OVERRIDES
    (* split methods *)
    &lt;* LL &gt;= {VBT.mu, SELF, ch} *&gt;
    beChild := BeChild;

    (* VBT down methods *)
    &lt;* LL.sup = VBT.mu.SELF *&gt;
    reshape := Reshape;
    repaint := Repaint;
    rescreen := Rescreen;
    &lt;* LL.sup = VBT.mu *&gt;
    mouse := Mouse;
    position := Position;

    (* VBT up methods *)
    &lt;* LL.sup = ch *&gt;
    setcage := SetCage;
    setcursor := SetCursor;
    paintbatch := PaintBatch;
    sync := Sync;
    capture := Capture;
    screenOf := ScreenOf;
  END;

VAR showSyncRect := FALSE;
</PRE><BLOCKQUOTE><EM> For debugging: when <CODE>showSyncRect</CODE> is true, a highlight rectangle is drawn
   to frame the rectangle that is copied when the on-screen VBT is updated
   from the off-screen VBT. </EM></BLOCKQUOTE><PRE>
</PRE> Split Method Implementations -------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="BeChild"><procedure>BeChild</procedure></A>(v: T; ch: VBT.T) RAISES {} =
  &lt;* LL &gt;= {VBT.mu, v, ch} *&gt;
  BEGIN
    Filter.T.beChild(v, ch);
    VBTClass.ClearShortCircuit(ch)
  END BeChild;
</PRE> Down Method Implementations --------------------------------------------- 

<P> In the down direction, argument points and rectangles must be translated
   from the parent's coordinate system to the child's coordinate system. This
   is accomplished by subtracting <CODE>delta(prnt)</CODE>. 

<P><PRE>PROCEDURE <A NAME="Reshape"><procedure>Reshape</procedure></A>(prnt: T; READONLY cd: VBT.ReshapeRec) RAISES {} =
</PRE><BLOCKQUOTE><EM> Create and install a new off-screen VBT with <CODE>prnt</CODE>'s width and height, set
   the new value <CODE>delta(prnt)</CODE>, and recursively reshape the child so it has
   the same domain as <CODE>offscreen(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu.prnt *&gt;
  VAR
    child := prnt.ch;
    delta := Rect.NorthWest(cd.new);
  BEGIN
    LOCK prnt DO prnt.delta := delta END;
    IF Rect.Congruent(cd.new, cd.prev)
       AND NOT Rect.IsEmpty(cd.new) THEN
      VAR offScreen: VBT.T; BEGIN
        LOCK prnt DO offScreen := prnt.offScreen END;
        PaintVBTtoVBT(prnt, cd.new, offScreen, delta);
        LOCK prnt DO prnt.screenDiff := Rect.Empty END
      END
    ELSE
      ReshapeOffScreen(prnt);
      ReshapeSavedBuff(prnt);
      IF child # NIL THEN
        VBTClass.Reshape(child, Rect.Sub(cd.new, delta), Rect.Empty)
      END
    END
  END Reshape;

PROCEDURE <A NAME="Repaint"><procedure>Repaint</procedure></A>(prnt: T; READONLY rgn: Region.T) RAISES {} =
</PRE><BLOCKQUOTE><EM> Merge <CODE>rgn</CODE> with <CODE>screenDiff(prnt)</CODE>, and then update the on-screen VBT. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu.prnt *&gt;
  BEGIN
    LOCK prnt DO
      prnt.screenDiff :=
        Rect.Join(prnt.screenDiff, Rect.Sub(rgn.r, prnt.delta))
    END;
    Update(prnt)
  END Repaint;

PROCEDURE <A NAME="Rescreen"><procedure>Rescreen</procedure></A>(prnt: T; READONLY cd: VBT.RescreenRec) =
</PRE><BLOCKQUOTE><EM> Cache the current screen-id, and then call the parent type's <CODE>rescreen</CODE>
   method. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu.prnt *&gt;
  VAR screen := Trestle.ScreenOf(prnt, Point.Origin); BEGIN
    LOCK prnt DO prnt.screenId := screen.id END;
    Filter.T.rescreen(prnt, cd)
  END Rescreen;

PROCEDURE <A NAME="Mouse"><procedure>Mouse</procedure></A>(prnt: T; READONLY cd: VBT.MouseRec) RAISES {} =
</PRE><BLOCKQUOTE><EM> If <CODE>NOT cd.cp.gone</CODE>, invoke the parent type's <CODE>mouse</CODE> method with the mouse
   location translated by <CODE>-delta(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR cdP: VBT.MouseRec; child := prnt.ch; BEGIN
    IF prnt.ch # NIL THEN
      cdP := cd;
      IF NOT cd.cp.gone THEN
        cdP.cp.pt := Point.Sub(cdP.cp.pt, prnt.delta)
      END;
      VBTClass.Mouse(child, cdP)
    END
  END Mouse;

PROCEDURE <A NAME="Position"><procedure>Position</procedure></A>(prnt: T; READONLY cd: VBT.PositionRec) RAISES {} =
</PRE><BLOCKQUOTE><EM> If <CODE>NOT cd.cp.offScreen</CODE>, invoke the parent type's <CODE>position</CODE> method with
   the mouse location translated by <CODE>-delta(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR cdP: VBT.PositionRec; child := prnt.ch; BEGIN
    IF prnt.ch # NIL THEN
      cdP := cd;
      IF NOT cd.cp.offScreen THEN
        cdP.cp.pt := Point.Sub(cd.cp.pt, prnt.delta)
      END;
      VBTClass.Position(child, cdP)
    END
  END Position;
</PRE> Up Method Implementations ----------------------------------------------- 

<P> In the up direction, argument points and rectangles must be translated
   from the child's coordinate system to the parent's coordinate system. This
   is accomplished by adding <CODE>delta(prnt)</CODE>. 

<P><PRE>PROCEDURE <A NAME="SetCage"><procedure>SetCage</procedure></A>(prnt: T; ch: VBT.T) RAISES {} =
</PRE><BLOCKQUOTE><EM> If the child <CODE>ch</CODE>'s cage is non-trivial and refers to the same screen as
   that of its parent <CODE>prnt</CODE>, then translate the cage to parent coordinates
   and recursively propagate the message up the VBT tree. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = ch *&gt;
  VAR cg := VBTClass.Cage(ch); BEGIN
    LOCK prnt DO
      IF cg.rect # Rect.Full AND prnt.screenId = cg.screen THEN
        cg.rect := Rect.Add(cg.rect, prnt.delta)
      END;
      VBTClass.SetCage(prnt, cg)
    END
  END SetCage;

PROCEDURE <A NAME="SetCursor"><procedure>SetCursor</procedure></A>(prnt: T; ch: VBT.T) RAISES {} =
  VAR cs := ch.getcursor();
  BEGIN (* LL=ch *)
    LOCK prnt DO
      IF cs # prnt.effectiveCursor THEN
        prnt.effectiveCursor := cs;
        IF prnt.parent # NIL THEN prnt.parent.setcursor(prnt) END
      END
    END
  END SetCursor;

PROCEDURE <A NAME="PaintBatch"><procedure>PaintBatch</procedure></A>(prnt: T; &lt;*UNUSED*&gt; ch: VBT.T; ba: Batch.T) RAISES {} =
</PRE><BLOCKQUOTE><EM> Merge <CODE>screenDiff(prnt)</CODE> with a bounding box of the painting commands <CODE>ba</CODE>;
   then forward the paint batch to <CODE>offscreen(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = ch *&gt;
  VAR offScreen: VBT.T; clip: Rect.T; BEGIN
    DblBufferUtil.Tighten(ba);
    clip := BatchUtil.GetClip(ba);
    LOCK prnt DO
      offScreen := prnt.offScreen;
      prnt.screenDiff := Rect.Join(prnt.screenDiff, clip);
      prnt.savedDiff := Rect.Join(prnt.savedDiff, clip)
    END;
    VBTClass.PaintBatch(offScreen, ba)
  END PaintBatch;

PROCEDURE <A NAME="Sync"><procedure>Sync</procedure></A>(prnt: T; &lt;*UNUSED*&gt; ch: VBT.T; wait: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> Update the on-screen VBT from <CODE>offscreen(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = ch *&gt;
  BEGIN
    Update(prnt, wait)
  END Sync;

PROCEDURE <A NAME="Capture"><procedure>Capture</procedure></A>(
    prnt: T;
    &lt;*UNUSED*&gt; ch: VBT.T;
    READONLY rect: Rect.T;
    VAR (*OUT*) br: Region.T)
    : ScrnPixmap.T RAISES {} =
</PRE><BLOCKQUOTE><EM> The rectangle <CODE>rect</CODE> is in <CODE>ch</CODE>'s coordinate system. Capture the rectangle
   <CODE>rect</CODE> from the VBT <CODE>offscreen(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = ch *&gt;
  VAR offScreen: VBT.T; BEGIN
    LOCK prnt DO offScreen := prnt.offScreen END;
    RETURN VBT.Capture(offScreen, rect, br)
  END Capture;

PROCEDURE <A NAME="ScreenOf"><procedure>ScreenOf</procedure></A>(
    prnt: T;
    &lt;*UNUSED*&gt; ch: VBT.T;
    READONLY pt: Point.T)
    : Trestle.ScreenOfRec RAISES {} =
</PRE><BLOCKQUOTE><EM> The point <CODE>pt</CODE> is in <CODE>ch</CODE>'s coordinate system. Recurse on <CODE>prnt</CODE> with the
   point <CODE>pt</CODE> translated by <CODE>delta(prnt)</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = ch *&gt;
  VAR delta: Point.T; BEGIN
    LOCK prnt DO delta := prnt.delta END;
    RETURN Trestle.ScreenOf(prnt, Point.Add(pt, delta));
  END ScreenOf;

PROCEDURE <A NAME="ForceBatches"><procedure>ForceBatches</procedure></A>(v: VBT.T): T =
</PRE><BLOCKQUOTE><EM> Force the paint batches of all ancestors of <CODE>v</CODE> up to a VBT of type <CODE>T</CODE>,
   and return that VBT. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; v *&gt;
  BEGIN
    WHILE NOT ISTYPE(v, T) DO
      LOCK v DO VBTRep.ForceBatch(v) END;
      v := VBT.Parent(v)
    END;
    &lt;* ASSERT v # NIL *&gt;
    RETURN v
  END ForceBatches;

PROCEDURE <A NAME="ClearSaved2"><procedure>ClearSaved2</procedure></A>(v: T) =
  &lt;* LL.sup &lt; v *&gt;
  VAR offScreen, savedBuff: VBT.T; BEGIN
    (* discard existing buffer (if any) *)
    LOCK v DO
      offScreen := v.offScreen;
      savedBuff := v.savedBuff
    END;
    IF savedBuff # NIL THEN
      Trestle.Delete(savedBuff);
      VBT.Discard(savedBuff)
    END;
    VAR savedDiff: Rect.T; BEGIN
      IF offScreen # NIL
        THEN savedDiff := VBT.Domain(offScreen)
        ELSE savedDiff := Rect.Full
      END;
      LOCK v DO
        v.savedBuff := NIL;
        v.savedDiff := savedDiff
      END
    END
  END ClearSaved2;

PROCEDURE <A NAME="ClearSaved"><procedure>ClearSaved</procedure></A>(v: VBT.T) =
  &lt;* LL.sup &lt; v *&gt;
  BEGIN
    WHILE NOT ISTYPE(v, T) DO v := VBT.Parent(v) END;
    &lt;* ASSERT v # NIL *&gt;
    ClearSaved2(v)
  END ClearSaved;

PROCEDURE <A NAME="Save"><procedure>Save</procedure></A>(v: VBT.T) =
  &lt;* LL.sup &lt; v *&gt;
  VAR
    db: T := ForceBatches(v);
    offscreen, savedBuff: VBT.T;
    savedDiff: Rect.T;
  BEGIN
    (* create a savedBuff if necessary *)
    LOCK db DO
      offscreen := db.offScreen;
      savedBuff := db.savedBuff;
      savedDiff := db.savedDiff
    END;
    IF offscreen = NIL THEN RETURN END;
    IF savedBuff = NIL THEN
      savedBuff := InstallOffscreen(db)
    END;
    PaintVBTtoVBT(savedBuff, savedDiff, offscreen);
    LOCK db DO
      db.savedBuff := savedBuff;
      db.savedDiff := Rect.Empty
    END
  END Save;

PROCEDURE <A NAME="Restore"><procedure>Restore</procedure></A>(v: VBT.T) =
  &lt;* LL.sup &lt; v *&gt;
  VAR
    db: T := ForceBatches(v);
    offscreen, savedBuff: VBT.T;
    savedDiff: Rect.T;
  BEGIN
    (* create a savedBuff if necessary *)
    LOCK db DO
      offscreen := db.offScreen;
      savedBuff := db.savedBuff;
      savedDiff := db.savedDiff
    END;
    IF offscreen = NIL THEN RETURN END;
    IF savedBuff = NIL
      THEN VBT.PaintTint(offscreen, savedDiff, PaintOp.Bg)
      ELSE PaintVBTtoVBT(offscreen, savedDiff, savedBuff)
    END;
    LOCK db DO
      db.screenDiff := Rect.Join(db.screenDiff, savedDiff);
      db.savedDiff := Rect.Empty
    END
  END Restore;
</PRE> Create/capture the off-screen VBT --------------------------------------- 

<P><PRE>PROCEDURE <A NAME="InstallOffscreen"><procedure>InstallOffscreen</procedure></A>(v: T): VBT.T =
</PRE><BLOCKQUOTE><EM> Install and return a new offscreen VBT whose domain is
   congruent to <CODE>v</CODE>'s. </EM></BLOCKQUOTE><PRE>
  VAR
    offScreen := NEW(VBT.Leaf);
    dom := VBT.Domain(v);
    tso := Trestle.ScreenOf(v, Point.Origin);
    trsl := tso.trsl;
    stInstall := VBT.ScreenTypeOf(InstalledVBT.Child(v));
    st := VBT.ScreenTypeOf(v);
  &lt;* FATAL TrestleComm.Failure *&gt;
  BEGIN
    IF trsl # NIL AND st # NIL THEN
      (* Install a Filter above &quot;offScreen&quot; so that it can have a ScreenType
         that Trestle likes in the case where &quot;v&quot; has an &quot;unusual&quot; screen
         type (i.e.  there is a scale filter) *)
      WITH filter = NEW(Filter.T).init(offScreen) DO
        Trestle.Attach(filter, trsl);
        Trestle.InstallOffscreen(
          filter, dom.east - dom.west, dom.south - dom.north, stInstall);
        IF filter.st # st THEN
          (* duke it out with trestle to set the screen type and domain *)
          VBTClass.Rescreen(offScreen, st);
          VBTClass.Reshape(offScreen, filter.domain, Rect.Empty);
        END
      END
    END;
    RETURN offScreen
  END InstallOffscreen;

PROCEDURE <A NAME="ReshapeOffScreen"><procedure>ReshapeOffScreen</procedure></A>(v: T) =
</PRE><BLOCKQUOTE><EM> Initialize <CODE>v</CODE>'s off-screen VBT. The VBT installed offscreen is a
   <CODE>Filter.T</CODE> containing a <CODE>VBT.Leaf</CODE>. The field <CODE>v.offScreen</CODE> is set to the
   leaf. The offscreen VBT is created with the same width and height as <CODE>v</CODE>.
   This procedure also has the side-effect of initializing <CODE>v.screenDiff</CODE>  and
   <CODE>v.savedDiff</CODE> to the full domain of the off-screen VBT. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu.v *&gt;
  VAR offScreen: VBT.T; BEGIN
    (* Delete and discard the current off-screen VBT (if any) *)
    LOCK v DO
      offScreen := v.offScreen;
      v.offScreen := NIL
    END;
    IF offScreen # NIL THEN
      Trestle.Delete(offScreen);
      VBT.Discard(offScreen)
    END;
    offScreen := InstallOffscreen(v);
    LOCK v DO
      v.offScreen := offScreen;
      v.screenDiff := VBT.Domain(offScreen);
      v.savedDiff := v.screenDiff
    END
  END ReshapeOffScreen;

PROCEDURE <A NAME="ReshapeSavedBuff"><procedure>ReshapeSavedBuff</procedure></A>(v: T) =
  VAR new, old: VBT.T; meet: Rect.T; BEGIN
    LOCK v DO old := v.savedBuff END;
    IF old = NIL THEN RETURN END;
    new := InstallOffscreen(v);
    (* copy the common areas *)
    meet := Rect.Meet(VBT.Domain(old), VBT.Domain(new));
    PaintVBTtoVBT(new, meet, old);
    (* fill the remaining area with background *)
    VAR a: Rect.Partition; BEGIN
      Rect.Factor(VBT.Domain(new), meet, (*out*) a, 0, 0);
      a[2] := a[4];
      FOR i := 0 TO 3 DO
        VBT.PaintTint(new, a[i], PaintOp.Bg)
      END
    END;
    (* discard the old buffer *)
    Trestle.Delete(old);
    VBT.Discard(old);
    LOCK v DO v.savedBuff := new END
  END ReshapeSavedBuff;

PROCEDURE <A NAME="PaintVBTtoVBT"><procedure>PaintVBTtoVBT</procedure></A>(to: VBT.T; clip: Rect.T;
  from: VBT.T; delta := Point.Origin; wait := TRUE) =
&lt;* LL.sup &lt; to *&gt;
  VAR dummy: Region.T; pixmap: ScrnPixmap.T; BEGIN
    pixmap := VBT.Capture(from, Rect.Sub(clip, delta), (*OUT*) dummy);
    IF pixmap # NIL THEN
      IF dummy = Region.Empty THEN
        VBT.PaintScrnPixmap(to, src := pixmap, delta := delta);
        VBT.Sync(to, wait)
      END;
      &lt;* FATAL TrestleComm.Failure *&gt;
      BEGIN pixmap.free() END
    END
  END PaintVBTtoVBT;

PROCEDURE <A NAME="LogBadRectArea"><procedure>LogBadRectArea</procedure></A>(&lt;*UNUSED*&gt; area: INTEGER) =
</PRE><BLOCKQUOTE><EM> This procedure exists soley to log the area of the rectangle copied from
   the off-screen VBT to the on-screen VBT at update time. </EM></BLOCKQUOTE><PRE>
  BEGIN END LogBadRectArea;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(v: T; wait := TRUE) =
</PRE><BLOCKQUOTE><EM> Update <CODE>v</CODE> from <CODE>offscreen(v)</CODE>, and set <CODE>screenDiff(v)</CODE> to <CODE>Rect.Empty</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; v *&gt;
  VAR screenDiff: Rect.T; offScreen: VBT.T; delta: Point.T; BEGIN
    LOCK v DO
      screenDiff := v.screenDiff;
      offScreen := v.offScreen;
      delta := v.delta
    END;
    IF offScreen # NIL AND screenDiff # Rect.Empty THEN
      VAR transScreenDiff := Rect.Add(screenDiff, delta); BEGIN
        PaintVBTtoVBT(v, transScreenDiff, offScreen, delta, wait);
        IF showSyncRect THEN
          HighlightVBT.SetRect(v, transScreenDiff);
          VBT.Sync(v)
        END
      END;
      LogBadRectArea(Rect.HorSize(screenDiff) * Rect.VerSize(screenDiff));
      LOCK v DO v.screenDiff := Rect.Empty END
    END
  END Update;

BEGIN
END DblBufferVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
