<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/stmts/IfStmt.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/stmts/IfStmt.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: IfStmt.m3                                             
 Last modified on Thu Nov 10 13:30:29 PST 1994 by kalsow     
      modified on Wed Feb 27 04:00:55 1991 by muller         

<P><PRE>MODULE <module><implements><A HREF="IfStmt.i3.html">IfStmt</A></implements></module>;

IMPORT <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="../exprs/Expr.i3.html">Expr</A>, <A HREF="../builtinTypes/Bool.i3.html">Bool</A>, <A HREF="#x1">Type</A>, <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="#x2">Token</A>, <A HREF="Stmt.i3.html">Stmt</A>, <A HREF="StmtRep.i3.html">StmtRep</A>, <A HREF="../misc/Scanner.i3.html">Scanner</A>, <A HREF="../builtinTypes/ErrType.i3.html">ErrType</A>;
FROM <A HREF="../misc/Scanner.i3.html">Scanner</A> IMPORT Match, GetToken, cur;

TYPE
  P = Stmt.T OBJECT
        clauses  : Clause;
        elseBody : Stmt.T;
      OVERRIDES
        check       := Check;
        compile     := Compile;
        outcomes    := GetOutcome;
      END;

TYPE
  Clause = REF RECORD
             origin : INTEGER;
             next   : Clause;
             cond   : Expr.T;
             body   : Stmt.T;
           END;

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A> (): Stmt.T =
  TYPE TK = Token.T;
  VAR p := NEW (P);  c, last: Clause;
  BEGIN
    StmtRep.Init (p);

    Match (TK.tIF);
    c := NEW (Clause);
    c.origin := Scanner.offset;
    c.next := NIL;
    c.cond := Expr.Parse ();
    Match (TK.tTHEN);
    c.body := Stmt.Parse ();
    p.clauses := c;
    p.elseBody := NIL;
    last := c;

    WHILE (cur.token = TK.tELSIF) DO
      GetToken (); (* ELSIF *)
      c := NEW (Clause);
      c.origin := Scanner.offset;
      c.next := NIL;
      c.cond := Expr.Parse ();
      Match (TK.tTHEN);
      c.body := Stmt.Parse ();
      last.next := c;
      last := c;
    END;

    IF (cur.token = TK.tELSE) THEN
      GetToken (); (* ELSE *)
      p.elseBody := Stmt.Parse ();
    END;

    Match (TK.tEND);
    RETURN p;
  END Parse;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (p: P;  VAR cs: Stmt.CheckState) =
  VAR c: Clause;  t: Type.T;
  BEGIN
    c := p.clauses;
    WHILE (c # NIL) DO
      Expr.TypeCheck (c.cond, cs);
      t := Expr.TypeOf (c.cond);
      IF (Type.Base (t) # Bool.T) AND (t # ErrType.T) THEN
        Scanner.offset := c.origin;
        Error.Msg (&quot;IF condition must be a BOOLEAN&quot;);
      END;
      Stmt.TypeCheck (c.body, cs);
      c := c.next;
    END;
    Stmt.TypeCheck (p.elseBody, cs);
  END Check;

PROCEDURE <A NAME="Compile"><procedure>Compile</procedure></A> (p: P): Stmt.Outcomes =
  VAR
    c       : Clause;
    l_end   : CG.Label;
    l_next  : CG.Label;
    oc, xc  : Stmt.Outcomes;
    gotoEnd := FALSE;
  BEGIN
    l_end := CG.Next_label ();

    c := p.clauses;
    oc := Stmt.Outcomes {};
    WHILE (c # NIL) DO
      l_next := CG.Next_label ();
      Scanner.offset := c.origin;
      CG.Gen_location (Scanner.offset);
      Expr.PrepBranch (c.cond, CG.No_label, l_next, CG.Always - CG.Likely);
      Expr.CompileBranch (c.cond, CG.No_label, l_next, CG.Always - CG.Likely);
      xc := Stmt.Compile (c.body);
      oc := oc + xc;
      IF (Stmt.Outcome.FallThrough IN xc)
        AND ((c.next # NIL) OR (p.elseBody # NIL)) THEN
        CG.Jump (l_end);
        gotoEnd := TRUE;
      END;
      CG.Set_label (l_next);
      c := c.next;
    END;

    IF (p.elseBody = NIL)
      THEN oc := oc + Stmt.Outcomes {Stmt.Outcome.FallThrough};
      ELSE oc := oc + Stmt.Compile (p.elseBody);
    END;

    IF (gotoEnd) THEN CG.Set_label (l_end) END;
    RETURN oc;
  END Compile;

PROCEDURE <A NAME="GetOutcome"><procedure>GetOutcome</procedure></A> (p: P): Stmt.Outcomes =
  VAR c: Clause;  oc := Stmt.Outcomes {};
  BEGIN
    c := p.clauses;
    WHILE (c # NIL) DO
      oc := oc + Stmt.GetOutcome (c.body);
      c := c.next;
    END;
    IF (p.elseBody = NIL)
      THEN oc := oc + Stmt.Outcomes {Stmt.Outcome.FallThrough};
      ELSE oc := oc + Stmt.GetOutcome (p.elseBody);
    END;
    RETURN oc;
  END GetOutcome;

BEGIN
END IfStmt.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Type is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Token is in:
</A><UL>
<LI><A HREF="../misc/Token.i3.html#0TOP0">m3front/src/misc/Token.i3</A>
<LI><A HREF="../../../mentor/src/parse/Token.i3.html#0TOP0">mentor/src/parse/Token.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
