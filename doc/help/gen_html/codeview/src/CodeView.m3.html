<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: codeview/src/CodeView.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>codeview/src/CodeView.m3</H2></A><HR>
<inModule>
<PRE></PRE> --------------------------------------------------------------------- 
 07-JUN-96  JK  Modified ParseAlg to replace @number tags with spaces. 
 --------------------------------------------------------------------- 
 Copyright (C) 1992, Digital Equipment Corporation 
 All rights reserved. 
 See the file COPYRIGHT for a full description. 
 Last modified on Fri Jun 28 00:44:29 PDT 1996 by mhb    
      modified on Wed Feb 23 08:08:08 PST 1994 by kalsow 
      modified on Wed Jun 23 14:24:07 PDT 1993 by steveg 
      modified on Tue Jan  5 20:04:15 PST 1993 by johnh 
      modified on Wed Aug 19 16:34:43 PDT 1992 by sclafani
<PRE>&lt;*PRAGMA LL*&gt;

MODULE <module><implements><A HREF="CodeView.i3.html">CodeView</A></implements></module>;

IMPORT <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../ui/src/split/BorderedVBT.i3.html">BorderedVBT</A>, <A HREF="../../libm3/src/types/ASCII.i3.html">ASCII</A>, <A HREF="../../vbtkit/src/color/ColorName.i3.html">ColorName</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>, <A HREF="../../ui/src/vbt/Font.i3.html">Font</A>, <A HREF="#x1">IntRef</A>,
       <A HREF="IntRefSort.i3.html">IntRefSort</A>, <A HREF="../../libm3/derived/IntRefTbl.i3.html">IntRefTbl</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>,
       <A HREF="../../formsvbt/src/RefListUtils.i3.html">RefListUtils</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../libm3/derived/TextRefTbl.i3.html">TextRefTbl</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>,
       <A HREF="TextRef.i3.html">TextRef</A>, <A HREF="TextRefSort.i3.html">TextRefSort</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>, <A HREF="../../ui/src/split/TextureVBT.i3.html">TextureVBT</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>,
       <A HREF="../../vbtkit/src/vtext/VText.i3.html">VText</A>, <A HREF="../../vbtkit/src/vtext/VTDef.i3.html">VTDef</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../ui/src/split/ZSplit.i3.html">ZSplit</A>;

&lt;* FATAL Rd.Failure, Wr.Failure, Thread.Alerted, Rd.EndOfFile *&gt;
&lt;* FATAL Lex.Error, FloatMode.Trap *&gt;
&lt;* FATAL VTDef.Error, Split.NotAChild *&gt;

TYPE
  ProcInfo = REF RECORD
                   source : TEXT;
                   offsets: IntRefTbl.T;
                 END;
  Position = REF RECORD start, end: CARDINAL;  END;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        procTable   : TextRefTbl.T;
        font        : Font.T;
        delta       : CARDINAL;
      OVERRIDES
        shape       := ZShape;
        enter       := Enter;
        exit        := Exit;
        at          := At;
        event       := Event;
        exitAll     := ExitAll;
        listNames   := ListNames;
        listRegions := ListRegions;
        init        := Init;
      END;

TYPE
  AlgVBT = TextPort.T OBJECT
             interval: VText.Interval;
             proc    : ProcInfo;
           OVERRIDES
             shape := Shape;
           END;

&lt;* FATAL ColorName.NotFound *&gt;
VAR
  replaceTags: BOOLEAN := FALSE;       (* JK, 07-JUN-96 *)

  highlightStyle := VText.MakeIntervalOptions (
                      VText.IntervalStyle.InverseStyle,
                      PaintOp.MakeColorScheme (
                        PaintOp.Fg, PaintOp.FromRGB (
                                        ColorName.ToRGB (&quot;LightGreen&quot;).r,
                                        ColorName.ToRGB (&quot;LightGreen&quot;).g,
                                        ColorName.ToRGB (&quot;LightGreen&quot;).b)),
                      PaintOp.bgFg, PaintOp.Bg);

PROCEDURE <A NAME="ZShape"><procedure>ZShape</procedure></A> (v: VBT.T; ax: Axis.T; n: CARDINAL): VBT.SizeRange =
  VAR res := ZSplit.T.shape (v, ax, n);
  BEGIN
    IF res.pref &lt; 100 THEN
      IF ax = Axis.T.Ver THEN res.pref := 200 ELSE res.pref := 400 END;
      IF res.pref &gt;= res.hi THEN res.hi := res.pref + 1; END;
    END;
    RETURN res;
  END ZShape;

PROCEDURE <A NAME="Shape"><procedure>Shape</procedure></A> (&lt;*UNUSED*&gt; v : VBT.T;
                 &lt;*UNUSED*&gt; ax: Axis.T;
                 &lt;*UNUSED*&gt; n : CARDINAL): VBT.SizeRange =
  VAR res: VBT.SizeRange;
  BEGIN
    res.pref := 2000;
    res.lo := res.pref;
    res.hi := res.lo + 1;
    RETURN res;
  END Shape;

PROCEDURE <A NAME="Enter"><procedure>Enter</procedure></A> (t: T; procedureName: TEXT; pauseTime := -1) =
  VAR
    algVBT: AlgVBT;
    point : Point.T;
    depth : INTEGER;
    pos   : Position;
    refany: REFANY;
  BEGIN
    IF NOT t.procTable.get (procedureName, refany) THEN RETURN; END;
    algVBT := NewAlgVBT (t, refany);
    depth := Split.NumChildren (t) - 1;
    point := Point.Add (
               Rect.NorthWest (ZSplit.GetParentDomain (t)),
               Point.FromCoords (t.delta * depth, t.delta * depth));
    ZSplit.InsertAt (t, BorderedVBT.New (algVBT, 0.5), point);
    IF algVBT.proc.offsets.get (0, refany) THEN
      pos := refany;
      VText.MoveInterval (algVBT.interval, pos.start, pos.end);
      VBT.Mark (algVBT);
      IF pauseTime &lt; 0 THEN pauseTime := t.pauseTime; END;
      Thread.Pause (FLOAT(pauseTime, LONGREAL));
    END;
  END Enter;

PROCEDURE <A NAME="Exit"><procedure>Exit</procedure></A> (t: T; pauseTime := -1) =
  BEGIN
    IF Split.NumChildren (t) &lt; 2 THEN RETURN; END;
    Split.Delete (t, Split.Succ (t, NIL));
    IF pauseTime &lt; 0 THEN pauseTime := t.pauseTime; END;
    Thread.Pause (FLOAT(pauseTime, LONGREAL));
  END Exit;

PROCEDURE <A NAME="At"><procedure>At</procedure></A> (t: T; highlight: CARDINAL; pauseTime := -1) =
  VAR
    algVBT: AlgVBT;
    pos   : Position;
    refany: REFANY;
  BEGIN
    IF Split.NumChildren (t) &lt; 2 THEN RETURN; END;
    algVBT := Split.Succ (Split.Succ (t, NIL), NIL);
    IF algVBT.proc.offsets.get (highlight, refany) THEN
      pos := refany;
      VText.MoveInterval (algVBT.interval, pos.start, pos.end);
      VBT.Mark (algVBT);
      IF pauseTime &lt; 0 THEN pauseTime := t.pauseTime; END;
      Thread.Pause (FLOAT(pauseTime, LONGREAL));
    END;
  END At;

PROCEDURE <A NAME="Event"><procedure>Event</procedure></A> (t            : T;
                 highlight           := 0;
                 pauseTime           := -1;
                 procedureName: TEXT := NIL ) =
  BEGIN
    IF procedureName # NIL THEN
      t.enter (procedureName, pauseTime);
    ELSIF highlight &lt; 0 THEN
      t.exit (pauseTime);
    ELSE
      t.at (highlight, pauseTime);
    END;
  END Event;

PROCEDURE <A NAME="ExitAll"><procedure>ExitAll</procedure></A> (t: T) =
  VAR
    bg := Split.Pred (t, NIL);
    ch := Split.Pred (t, bg);
  BEGIN
    WHILE ch # NIL DO Split.Delete (t, ch); ch := Split.Pred (t, bg); END;
  END ExitAll;

PROCEDURE <A NAME="NewAlgVBT"><procedure>NewAlgVBT</procedure></A> (t: T; proc: ProcInfo): AlgVBT =
  VAR
    vbt: AlgVBT;
    vt : VText.T;
  BEGIN
    vbt := NEW (AlgVBT).init (wrap := FALSE, font := t.font);
    TextPort.SetText (vbt, proc.source);
    vbt.setReadOnly(TRUE);  (* replaces TextPort.SetReadOnly (vbt, TRUE);*)
    vt := TextPort.GetVText (vbt);
    vbt.interval := VText.CreateInterval (vt, 0, 0, highlightStyle);
    VText.SwitchInterval (vbt.interval, VText.OnOffState.On);
    vbt.proc := proc;
    RETURN vbt;
  END NewAlgVBT;

PROCEDURE <A NAME="Dump"><procedure>Dump</procedure></A> (source: Rd.T; wr: Wr.T; errorWr: Wr.T := NIL) =
  VAR
    procList: RefList.T;
    assoc   : RefList.T;
    name    : TEXT;
    proc    : ProcInfo;
    posList : RefList.T;
    pos     : Position;
    line    : REF INTEGER;
  BEGIN
    procList := SortTextRefTbl(ParseAlg (source, errorWr));
    WHILE procList # NIL DO
      assoc := RefListUtils.Pop (procList);
      name := RefListUtils.Pop (assoc);
      proc := RefListUtils.Pop (assoc);
      Wr.PutText (wr, name &amp; &quot;\n&quot;);
      posList := SortIntRefTbl(proc.offsets);
      WHILE posList # NIL DO
        assoc := RefListUtils.Pop (posList);
        line := RefListUtils.Pop (assoc);
        pos := RefListUtils.Pop (assoc);
        Wr.PutText (wr, Fmt.F (&quot;%5s  %s\n&quot;, Fmt.Int (line^),
                               Text.Sub (proc.source, pos.start,
                                         pos.end - pos.start)));
      END;
      Wr.PutChar (wr, '\n');
    END;
  END Dump;

PROCEDURE <A NAME="ParseAlg"><procedure>ParseAlg</procedure></A> (rd: Rd.T; errorWr: Wr.T): TextRefTbl.T =
  TYPE
    State = {Top, TopAt, TopTag, InProc, ProcAt, ProcTag, StatTag, InStat,
             StatAt};
  VAR
    procTable           := NEW(TextRefTbl.Default).init();
    procWr              := TextWr.New ();
    tagWr               := TextWr.New ();
    state               := State.Top;
    c        : CHAR;
    name     : TEXT;
    tag      : TEXT;
    id       : CARDINAL;
    any      : REFANY;
    proc     : ProcInfo;
    pos      : Position;
  BEGIN
    IF errorWr = NIL THEN errorWr := Stdio.stderr; END;
    WHILE NOT Rd.EOF (rd) DO
      c := Rd.GetChar (rd);
      CASE state OF
      | State.Top =&gt; IF c = '@' THEN state := State.TopAt; END;
      | State.TopAt =&gt;
          IF c IN ASCII.AlphaNumerics THEN
            Wr.PutChar (tagWr, c);
            state := State.TopTag;
          ELSE
            state := State.Top;
          END;
      | State.TopTag =&gt;
          IF c IN ASCII.Punctuation + ASCII.Spaces THEN
            name := TextWr.ToText (tagWr);
            proc := NEW (ProcInfo);
            proc.offsets := NEW(IntRefTbl.Default).init(4);
            pos := NEW (Position);
            tag := &quot;0&quot;;
            id := 0;
            pos.start := Wr.Index (procWr);
            state := State.InStat;
          ELSE
            Wr.PutChar (tagWr, c);
          END;
      | State.InProc =&gt;
          IF c = '@' THEN
            IF replaceTags THEN
              pos := NEW (Position);            (* JK, 11-JUN-96 *)
              pos.start := Wr.Index (procWr);   (* JK, 11-JUN-96 *)
              Wr.PutChar (procWr, ' ');	        (* JK, 07-JUN-96 *)
            END;
            state := State.ProcAt;
          ELSE
            Wr.PutChar (procWr, c);
          END;
      | State.ProcAt =&gt;
          IF c IN ASCII.Letters THEN
            Wr.PutChar (tagWr, c);
            state := State.ProcTag;
          ELSIF c IN ASCII.Digits THEN
            Wr.PutChar (tagWr, c);
            IF replaceTags THEN
              Wr.PutChar (procWr, ' ');	        (* JK, 07-JUN-96 *)
            END;
            state := State.StatTag;
          ELSE
            state := State.InProc;
          END;
      | State.ProcTag =&gt;
          IF c IN ASCII.Punctuation + ASCII.Spaces THEN
            tag := TextWr.ToText (tagWr);
            IF NOT Text.Equal (tag, name) THEN
              Wr.PutText (
                errorWr,
                Fmt.F (
                  &quot;procedure trailer for '%s' does not match header\n&quot;,
                  name));
            END;
            proc.source := TextWr.ToText (procWr);
            EVAL procTable.put (name, proc);
            state := State.Top;
          ELSE
            Wr.PutChar (tagWr, c);
          END;
      | State.StatTag =&gt;
          IF c IN ASCII.Digits THEN
            IF replaceTags THEN
              Wr.PutChar (procWr, ' ');         (* JK, 07-JUN-96 *)
            END;
            Wr.PutChar (tagWr, c);
          ELSE
            IF replaceTags THEN
              Wr.PutChar (procWr, ' ');         (* JK, 07-JUN-96 *)
            END;
            tag := TextWr.ToText (tagWr);
            id := Lex.Int (TextRd.New (tag));
            IF proc.offsets.get (id, any) THEN
              Wr.PutText (
                errorWr,
                Fmt.F (
                  &quot;duplicate statement tag '@%s' at offsets %s and %s\n&quot;,
                  tag, Fmt.Int (pos.start), Fmt.Int (Rd.Index (rd))));
            END;
            IF NOT replaceTags THEN
              pos := NEW (Position);             (* JK, 11-JUN-96 *)
              pos.start := Wr.Index (procWr);    (* JK, 11-JUN-96 *)
            END;
            state := State.InStat;
          END;
      | State.InStat =&gt;
          IF c = '@' THEN
            state := State.StatAt;
          ELSE
            Wr.PutChar (procWr, c);
          END;
      | State.StatAt =&gt;
          IF c = '@' THEN
            Wr.PutChar (procWr, c);
            state := State.InStat;
          ELSE
            pos.end := Wr.Index (procWr);
            EVAL proc.offsets.put (id, pos);
            Wr.PutChar (procWr, c);
            state := State.InProc;
          END;
      END;
    END;

    CASE state OF
    | State.TopTag =&gt;
        Wr.PutText (
          errorWr, &quot;unterminated procedure header (@name) at end-of-file\n&quot;);
    | State.InProc, State.ProcAt =&gt;
        Wr.PutText (errorWr,
                    Fmt.F (
                      &quot;unmatched procedure header (@%s) at end-of-file\n&quot;,
                      name));
    | State.ProcTag =&gt;
        Wr.PutText (
          errorWr,
          Fmt.F (
            &quot;unterminated procedure trailer for '%s' at end-of-file\n&quot;,
            name));
    | State.StatTag =&gt;
        Wr.PutText (
          errorWr,
          Fmt.F (&quot;unterminated statement tag for '%s' at end-of-file\n&quot;,
                 name));
    | State.InStat =&gt;
        Wr.PutText (
          errorWr,
          Fmt.F (&quot;unterminated statement marker ('@%s') at end-of-file\n&quot;,
                 tag));
        Wr.PutText (errorWr,
                    Fmt.F (
                      &quot;unmatched procedure header (@%s) at end-of-file\n&quot;,
                      name));
    | State.StatAt =&gt;
        pos.end := Wr.Index (procWr);
        EVAL proc.offsets.put (Lex.Int (TextRd.New (tag)), pos);
        Wr.PutText (errorWr,
                    Fmt.F (
                      &quot;unmatched procedure header (@%s) at end-of-file\n&quot;,
                      name));
    ELSE
    END;
    Wr.Flush (errorWr);
    RETURN procTable;
  END ParseAlg;

PROCEDURE <A NAME="ListNames"><procedure>ListNames</procedure></A> (t: T): RefList.T =
  VAR
    iter            := t.procTable.iterate();
    k   : TEXT;
    val : REFANY;
    res : RefList.T := NIL;
  BEGIN
    WHILE iter.next(k, val) DO res := RefList.Cons(k, res); END;
    RETURN res
  END ListNames;

PROCEDURE <A NAME="ListRegions"><procedure>ListRegions</procedure></A> (t: T; procedureName: TEXT): RefList.T =
  VAR
    refany: REFANY;
    proc  : ProcInfo;
    k     : INTEGER;
    ri    : REF INTEGER;
    val   : REFANY;
    res   : RefList.T   := NIL;
  BEGIN
    IF t.procTable.get(procedureName, refany) THEN
      proc := refany;
      WITH iter = proc.offsets.iterate() DO
        WHILE iter.next(k, val) DO
          ri := NEW(REF INTEGER);
          ri^ := k;
          res := RefList.Cons(ri, res);
        END;
      END;
      RETURN res
    ELSE
      RETURN NIL;
    END;
  END ListRegions;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (         t          : T;
                         source     : Rd.T;
                         errorWr    : Wr.T     := NIL;
                READONLY fontName              := DefaultFont;
                         paneOffset : CARDINAL := 20;
                         background : VBT.T    := NIL ): T =
  BEGIN
    IF background = NIL THEN
      background :=
        BorderedVBT.New(TextureVBT.New(txt := Pixmap.Gray), 0.5);
    END;
    EVAL ZSplit.T.init(t, background);
    t.procTable := ParseAlg(source, errorWr);
    t.font := Font.FromName(fontName);
    t.delta := paneOffset;
    RETURN t;
  END Init;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (         source     : Rd.T;
                        errorWr    : Wr.T     := NIL;
               READONLY fontName              := DefaultFont;
                        paneOffset : CARDINAL := 20;
                        background : VBT.T    := NIL ): T =
  BEGIN
    RETURN Init(NEW(T), source, errorWr, fontName, paneOffset,
                background);
  END New;

PROCEDURE <A NAME="SortTextRefTbl"><procedure>SortTextRefTbl</procedure></A>(tbl: TextRefTbl.T): RefList.T =
  VAR arr  := NEW(REF ARRAY OF TextRef.T, tbl.size());
      iter := tbl.iterate();
      k: TEXT;
      val: REFANY;
      res: RefList.T := NIL;
  BEGIN
    FOR i := 0 TO LAST(arr^) DO
      EVAL iter.next(k, val);
      arr[i] := TextRef.T{k, val};
    END;
    TextRefSort.Sort(arr^);
    FOR i := 0 TO LAST(arr^) DO
      res := RefList.Cons(RefList.List2(arr[i].key, arr[i].value), res);
    END;
    res := RefList.ReverseD(res);
    RETURN res;
  END SortTextRefTbl;

PROCEDURE <A NAME="SortIntRefTbl"><procedure>SortIntRefTbl</procedure></A> (tbl: IntRefTbl.T): RefList.T =
  VAR
    arr               := NEW(REF ARRAY OF IntRef.T, tbl.size());
    iter              := tbl.iterate();
    k   : INTEGER;
    ri  : REF INTEGER;
    val : REFANY;
    res : RefList.T   := NIL;
  BEGIN
    FOR i := 0 TO LAST(arr^) DO
      EVAL iter.next(k, val);
      arr[i] := IntRef.T{k, val};
    END;
    IntRefSort.Sort(arr^);
    FOR i := 0 TO LAST(arr^) DO
      ri := NEW(REF INTEGER);
      ri^ := arr[i].key;
      res := RefList.Cons(RefList.List2(ri, arr[i].value), res);
    END;
    res := RefList.ReverseD(res);
    RETURN res;
  END SortIntRefTbl;

PROCEDURE <A NAME="DoReplaceTags"><procedure>DoReplaceTags</procedure></A>( replace: BOOLEAN := TRUE ) =
  BEGIN
    replaceTags := replace;
  END DoReplaceTags;

BEGIN
END CodeView.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface IntRef is in:
</A><UL>
<LI><A HREF="../../cit_util/derived/IntRef.i3.html#0TOP0">cit_util/derived/IntRef.i3</A>
<LI><A HREF="IntRef.i3.html#0TOP0">codeview/src/IntRef.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
