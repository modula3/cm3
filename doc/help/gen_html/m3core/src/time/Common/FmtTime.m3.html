<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/time/Common/FmtTime.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/time/Common/FmtTime.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1989, Digital Equipment Corporation 
 All rights reserved. 
 See the file COPYRIGHT for a full description. 
 Last modified on Wed Jun 22 14:52:00 PDT 1994 by kalsow  
      modified on Sat Dec  5 11:54:29 PST 1992 by mcjones 

<P><PRE>MODULE <module><implements><A HREF="FmtTime.i3.html">FmtTime</A></implements></module>;

IMPORT <A HREF="Date.i3.html">Date</A>, <A HREF="Time.i3.html">Time</A>, <A HREF="../../convert/Convert.i3.html">Convert</A>, <A HREF="../../text/Text.i3.html">Text</A>;

CONST
  MaxBuf = 4  (* WeekDay *)
         + 4  (* Month *)
         + 3  (* date *)
         + 3  (* hour *)
         + 3  (* minutes *)
         + 3  (* seconds *)
         + 48 (* time zone *)
         + 5  (* year *)
         ;

TYPE
  Buffer = RECORD
    len: INTEGER;
    buf: ARRAY [0..MaxBuf] OF CHAR;
  END;

PROCEDURE <A NAME="Long"><procedure>Long</procedure></A>(t: Time.T; z: Date.TimeZone := NIL): TEXT =
  BEGIN
    RETURN DateLong(Date.FromTime(t, z))
  END Long;

PROCEDURE <A NAME="Short"><procedure>Short</procedure></A>(t: Time.T; z: Date.TimeZone := NIL): TEXT =
  BEGIN
    RETURN DateShort(Date.FromTime(t, z))
  END Short;

PROCEDURE <A NAME="DateLong"><procedure>DateLong</procedure></A>(READONLY d: Date.T): TEXT =
  VAR b: Buffer;
  BEGIN
    b.len := 0;
    AddText (b, WeekDay[d.weekDay]);
    AddText (b, Month[d.month]);
    AddInt  (b, d.day,    2, ' ', ' ');
    AddInt  (b, d.hour,   2, '0', ':');
    AddInt  (b, d.minute, 2, '0', ':');
    AddInt  (b, d.second, 2, '0', ' ');
    AddText (b, d.zone);
    AddInt  (b, d.year,   4, ' ', ' ');
    RETURN Text.FromChars (SUBARRAY (b.buf, 0, b.len-1));
  END DateLong;

PROCEDURE <A NAME="DateShort"><procedure>DateShort</procedure></A>(READONLY d: Date.T): TEXT =
  VAR b: Buffer;
  BEGIN
    b.len := 0;
    AddText (b, Month[d.month]);
    AddInt  (b, d.day,    2, ' ', ' ');
    AddInt  (b, d.hour,   2, '0', ':');
    AddInt  (b, d.minute, 2, '0', ' ');
    RETURN Text.FromChars (SUBARRAY (b.buf, 0, b.len-1));
  END DateShort;

PROCEDURE <A NAME="AddText"><procedure>AddText</procedure></A> (VAR b: Buffer;  txt: TEXT) =
  BEGIN
    Text.SetChars (SUBARRAY (b.buf, b.len, NUMBER (b.buf) - b.len), txt);
    INC (b.len, Text.Length (txt));
    b.buf[b.len] := ' ';  INC (b.len);
  END AddText;

PROCEDURE <A NAME="AddInt"><procedure>AddInt</procedure></A> (VAR b: Buffer;  value, width: INTEGER;  pre, post: CHAR) =
  &lt;*FATAL Convert.Failed*&gt;
  VAR
    buf : ARRAY [0..BITSIZE(INTEGER)] OF CHAR;
    len := Convert.FromInt (buf, value);
  BEGIN
    WHILE (width &gt; len) DO
      b.buf[b.len] := pre;  INC (b.len);
      DEC (width);
    END;
    FOR i := 0 TO len-1 DO
      b.buf[b.len] := buf[i];  INC (b.len);
    END;
    b.buf[b.len] := post;  INC (b.len);
  END AddInt;

BEGIN
END FmtTime.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
