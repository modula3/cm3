<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/runtime/LINUXLIBC6/RTHeapDep.m3</TITLE>
</HEAD>
<BODY bgcolor="#eeeeee">
<A NAME="0TOP0">
<H2>m3core/src/runtime/LINUXLIBC6/RTHeapDep.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation        
 All rights reserved.                                     
 See the file COPYRIGHT for a full description.           
                                                          
| Last modified on Fri Jan 20 09:39:09 PST 1995 by kalsow 
|      modified on Thu Jan 28 19:24:55 PST 1993 by jdd    

<P><PRE>UNSAFE MODULE <module><implements><A HREF="../common/RTHeapDep.i3.html">RTHeapDep</A></implements></module>;

IMPORT <A HREF="../POSIX/RT0u.i3.html">RT0u</A>, <A HREF="RTMachine.i3.html">RTMachine</A>, <A HREF="../common/RTHeapRep.i3.html">RTHeapRep</A>, <A HREF="../common/RTCollectorSRC.i3.html">RTCollectorSRC</A>, <A HREF="../common/RTVM.i3.html">RTVM</A>;
IMPORT <A HREF="../../C/Common/Cstdlib.i3.html">Cstdlib</A>, <A HREF="../../C/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="../../unix/linux-libc6/Umman.i3.html">Umman</A>, <A HREF="../../unix/linux-libc6/Unix.i3.html">Unix</A>, <A HREF="../../unix/linux-libc6/Uresource.i3.html">Uresource</A>, <A HREF="../../unix/linux-libc6/Usignal.i3.html">Usignal</A>, <A HREF="../../unix/linux-libc6/Utypes.i3.html">Utypes</A>, <A HREF="../../word/Word.i3.html">Word</A>;

VAR
  initialized                           := FALSE;
  defaultActionSIGSEGV: Usignal.SignalActionHandler := NIL;
  defaultSIGSEGV: Usignal.SignalHandler := NIL; (* previous handler *)

PROCEDURE <A NAME="Protect"><procedure>Protect</procedure></A> (p: Page; n: CARDINAL; readable, writable: BOOLEAN) =
  BEGIN
    IF NOT initialized THEN Init(); initialized := TRUE; END;
    IF NOT readable THEN writable := FALSE; END; (* processor limitation *)
    VAR prot: Ctypes.int := 0;
    BEGIN
      IF readable THEN prot := Word.Or(prot, Umman.PROT_READ); END;
      IF writable THEN prot := Word.Or(prot, Umman.PROT_WRITE); END;
      VAR
        ret := Umman.mprotect(LOOPHOLE(p * BytesPerPage, Utypes.caddr_t),
                              n * BytesPerPage, prot);
      BEGIN
        &lt;* ASSERT ret = 0 *&gt;
      END;
    END;
  END Protect;
</PRE> Init establishes a handler for SIGSEGV, caused by VM faults, and for all
   other signals that cause core dumps. 

<P><PRE>PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> () =
  BEGIN
    (* sanity check *)
    VAR vmPageBytes := Unix.getpagesize();
    BEGIN
      &lt;* ASSERT BytesPerPage &gt;= vmPageBytes *&gt;
      &lt;* ASSERT BytesPerPage MOD vmPageBytes = 0 *&gt;
    END;

    (* establish SIGSEGV handler; remember previous handler *)
    VAR
      newHandler := LOOPHOLE(Fault,Usignal.SignalActionHandler);
      vec := Usignal.struct_sigaction{
               sa_handler := newHandler,
               sa_mask := Usignal.empty_sigset_t,
               sa_flags := Usignal.SA_RESTART,
               sa_restorer := NIL};
      ovec: Usignal.struct_sigaction;
      ret, tmp: Ctypes.int;

    BEGIN
      vec.sa_mask.val[0] := Word.LeftShift(1, Usignal.SIGVTALRM - 1);
      tmp := Usignal.SA_RESTART;
      ret := Usignal.sigaction(Usignal.SIGSEGV, ADR(vec), ADR(ovec));
      &lt;* ASSERT ret = 0 *&gt;
      defaultActionSIGSEGV := ovec.sa_handler;
      defaultSIGSEGV := LOOPHOLE(defaultActionSIGSEGV,Usignal.SignalHandler);
    END;

    (* establish signal handler for all other signals that dump core, if no
       handler exists *)
    PROCEDURE OverrideDefault (sig: Ctypes.int) =
      VAR
        newHandler := LOOPHOLE(Core,Usignal.SignalActionHandler);
        vec := Usignal.struct_sigaction{
                 sa_handler := newHandler,
                 sa_mask := Usignal.empty_sigset_t,
                 sa_flags := Usignal.SA_RESTART,
                 sa_restorer := NIL};
        ovec: Usignal.struct_sigaction;
        ret := Usignal.sigaction(sig, ADR(vec), ADR(ovec));
      BEGIN
        vec.sa_mask.val[0] := Word.LeftShift(1, Usignal.SIGVTALRM - 1);
        &lt;* ASSERT ret = 0 *&gt;
        IF ovec.sa_handler # Usignal.SIG_DFL THEN
          ret := Usignal.sigaction(sig, ADR(ovec), ADR(vec));
          &lt;* ASSERT ret = 0 *&gt;
        END;
      END OverrideDefault;
    BEGIN
      OverrideDefault(Usignal.SIGQUIT);
      OverrideDefault(Usignal.SIGILL);
      OverrideDefault(Usignal.SIGTRAP);
      OverrideDefault(Usignal.SIGIOT);
      OverrideDefault(Usignal.SIGEMT);
      OverrideDefault(Usignal.SIGFPE);
      OverrideDefault(Usignal.SIGBUS);
      OverrideDefault(Usignal.SIGSYS);
    END;
  END Init;
</PRE> Fault is called upon a SIGSEGV signal, caused by a VM fault.  If
   RTHeapRep.Fault is not able to handle the fault, it invokes the previous
   action. 

<P><PRE>PROCEDURE <A NAME="Fault"><procedure>Fault</procedure></A> (sig : Ctypes.int;
                 scp : Usignal.struct_sigcontext;
                 code: Ctypes.int) =
  BEGIN
    IF RTHeapRep.Fault(LOOPHOLE(scp.cr2, ADDRESS)) THEN
      RETURN;
    END;
    IF defaultActionSIGSEGV = Usignal.SIG_IGN THEN RETURN; END;
    IF defaultActionSIGSEGV = Usignal.SIG_DFL THEN
      Core(sig, scp, code);
    ELSE
      defaultSIGSEGV(sig, scp, code);
    END;
  END Fault;
</PRE> Core is a signal handler for signals that dump core, to complete the
   current collection before dumping core.  This makes core files easier to
   debug, and avoids an Ultrix bug that creates incomplete core files if
   heap pages are read-protected. 

<P><PRE>VAR dumped_core := FALSE;

PROCEDURE <A NAME="Core"><procedure>Core</procedure></A> (             sig : Ctypes.int;
                &lt;* UNUSED *&gt; scp : Usignal.struct_sigcontext;
                &lt;* UNUSED *&gt; code: Ctypes.int) =
  VAR
    ovec: Usignal.struct_sigaction;
    vec := Usignal.struct_sigaction{sa_handler := Usignal.SIG_DFL,
        sa_mask := Usignal.empty_sigset_t,
        sa_flags := Usignal.SA_RESTART, sa_restorer := NIL};
  BEGIN
    INC(RT0u.inCritical);
    IF NOT dumped_core THEN
      dumped_core := TRUE;
      EVAL RTHeapRep.Crash();      (* clean up the heap *)
      EVAL Usignal.sigaction(sig, ADR(vec), ADR(ovec)); (* establish default action *)
      EVAL Usignal.sigsetmask(0);
      (** EVAL Usignal.kill(Uprocess.getpid(), sig); (* dump core *) **)
      Cstdlib.abort (); (* dump core *)
      &lt;* ASSERT FALSE *&gt;
    END;
    DEC(RT0u.inCritical);
  END Core;
</PRE> System-call faults are handled in RTHeapDepC.c 

<P><PRE>PROCEDURE <A NAME="TimeUsed"><procedure>TimeUsed</procedure></A> (): REAL =
  VAR usage: Uresource.struct_rusage;
  BEGIN
    VAR ret := Uresource.getrusage(Uresource.RUSAGE_SELF, ADR(usage));
    BEGIN
      &lt;* ASSERT ret # -1 *&gt;
    END;
    RETURN (FLOAT(usage.ru_utime.tv_sec)
              + FLOAT(usage.ru_utime.tv_usec) / 1000000.0)
             + (FLOAT(usage.ru_utime.tv_sec)
                  + FLOAT(usage.ru_utime.tv_usec) / 1000000.0);
  END TimeUsed;

PROCEDURE <A NAME="VMFaultTime"><procedure>VMFaultTime</procedure></A> (): REAL =
  BEGIN
    RETURN 0.010;                (* guess 10ms to handle a page fault *)
  END VMFaultTime;

BEGIN
  VM := RTVM.VMHeap();
  IF VM THEN
    RTMachine.RTHeapRep_Fault  := LOOPHOLE (RTHeapRep.Fault, ADDRESS);
    RTMachine.RTCSRC_FinishVM  := LOOPHOLE (RTCollectorSRC.FinishVM, ADDRESS);
  END;
END RTHeapDep.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
