<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tk/src/sem/M3CDef.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tk/src/sem/M3CDef.m3</H2></A><HR>
<inModule>
<PRE>MODULE <module><implements><A HREF="M3CDef.i3.html">M3CDef</A></implements></module>;
</PRE>*************************************************************************
                      Copyright (C) Olivetti 1989                        
                          All Rights reserved                            
                                                                         
 Use and copy of this software and preparation of derivative works based 
 upon this software are permitted to any person, provided this same      
 copyright notice and the following Olivetti warranty disclaimer are      
 included in any copy of the software or any modification thereof or     
 derivative work therefrom made by any person.                           
                                                                         
 This software is made available AS IS and Olivetti disclaims all        
 warranties with respect to this software, whether expressed or implied  
 under any law, including all implied warranties of merchantibility and  
 fitness for any purpose. In no event shall Olivetti be liable for any   
 damages whatsoever resulting from loss of use, data or profits or       
 otherwise arising out of or in connection with the use or performance   
 of this software.                                                       
*************************************************************************

<P> Copyright (C) 1991, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P><PRE>IMPORT <A HREF="../gast/AST.i3.html">AST</A>, <A HREF="../ast/M3AST_AS.i3.html">M3AST_AS</A>,  <A HREF="../ast/M3AST_SM.i3.html">M3AST_SM</A>, <A HREF="../gast/ASTWalk.i3.html">ASTWalk</A>;

IMPORT <A HREF="../ast/M3AST_AS_F.i3.html">M3AST_AS_F</A>, <A HREF="../ast/M3AST_SM_F.i3.html">M3AST_SM_F</A>, <A HREF="../ast/M3AST_TM_F.i3.html">M3AST_TM_F</A>;

IMPORT <A HREF="../../derived/SeqM3AST_AS_Binding.i3.html">SeqM3AST_AS_Binding</A>, <A HREF="../../derived/SeqM3AST_AS_STM.i3.html">SeqM3AST_AS_STM</A>, <A HREF="../../derived/SeqM3AST_AS_Actual.i3.html">SeqM3AST_AS_Actual</A>,
    <A HREF="../../derived/SeqM3AST_AS_CONS_ELEM.i3.html">SeqM3AST_AS_CONS_ELEM</A>;
IMPORT <A HREF="../toolmisc/M3Context.i3.html">M3Context</A>;

IMPORT <A HREF="M3CScope.i3.html">M3CScope</A>, <A HREF="../toolmisc/M3Error.i3.html">M3Error</A>, (* M3Assert, *) <A HREF="M3CSearch.i3.html">M3CSearch</A>, <A HREF="M3CTypesMisc.i3.html">M3CTypesMisc</A>, <A HREF="M3CExpsMisc.i3.html">M3CExpsMisc</A>;

PROCEDURE <A NAME="PopWithBindings"><procedure>PopWithBindings</procedure></A>(
    bindings: SeqM3AST_AS_Binding.T;
    i: SeqM3AST_AS_Binding.Iter)
    RAISES {}=
  VAR
    b: M3AST_AS.Binding;
  BEGIN
    IF SeqM3AST_AS_Binding.Next(i, b) THEN
      PopWithBindings(bindings, i);
      M3CScope.DefId(b.as_id, M3CScope.Change.Exit);
    END; (* if *)
  END PopWithBindings;

PROCEDURE <A NAME="For"><procedure>For</procedure></A>(cl: ASTWalk.Closure; f: M3AST_AS.For_st) RAISES {}=
  VAR
    iter: SeqM3AST_AS_STM.Iter;
    stm: M3AST_AS.STM;
  BEGIN
    ASTWalk.IgnoreChildren(cl);
    ASTWalk.ModeVisitNodes(		 &lt;* NOWARN *&gt;
        f.as_from, SetPass1Closure(),
        ASTWalk.OnEntryAndExit);
    ASTWalk.ModeVisitNodes(              &lt;* NOWARN *&gt;
        f.as_to, SetPass1Closure(),
        ASTWalk.OnEntryAndExit);
    IF f.as_by # NIL THEN
      ASTWalk.ModeVisitNodes(            &lt;* NOWARN *&gt;
          f.as_by, SetPass1Closure(),
          ASTWalk.OnEntryAndExit);
    END;
    M3CScope.DefId(f.as_id, M3CScope.Change.Enter);
    iter := SeqM3AST_AS_STM.NewIter(f.as_stm_s);
    WHILE SeqM3AST_AS_STM.Next(iter, stm) DO
      ASTWalk.ModeVisitNodes(            &lt;* NOWARN *&gt;
          stm, SetPass1Closure(),
          ASTWalk.OnEntryAndExit);
    END; (* while *)
    M3CScope.DefId(f.as_id, M3CScope.Change.Exit);
  END For;

PROCEDURE <A NAME="SetPass1Closure"><procedure>SetPass1Closure</procedure></A>(): ASTWalk.Closure RAISES {}=
  TYPE T = ASTWalk.Closure OBJECT OVERRIDES  callback := SetPass1 END;
  BEGIN
    RETURN NEW(T).init();
  END SetPass1Closure;

PROCEDURE <A NAME="SetPass1"><procedure>SetPass1</procedure></A>(cl: ASTWalk.Closure;
    a: AST.NODE;
    e: ASTWalk.VisitMode)
    RAISES {}=
  VAR
    m: M3CScope.Change;
  BEGIN
    IF e = ASTWalk.VisitMode.Entry THEN
      m := M3CScope.Change.Enter;
    ELSE
      m := M3CScope.Change.Exit;
    END;
    TYPECASE a OF
    | M3AST_AS.Compilation_Unit(cu) =&gt;
          (* If is the standard unit we call 'M3CScope.Standard' instead of
          'M3CScope.CompUnit' *)
          IF cu = M3Context.Standard() THEN
            IF m = M3CScope.Change.Enter THEN M3CScope.Standard(cu) END;
          ELSE
            M3CScope.CompilationUnit(cu, m)
          END;
          (* We dont want to try to resolve imports/exports, so we truncate
          this walk and start a recursive walk of the block.  *)
          IF m = M3CScope.Change.Enter THEN
            ASTWalk.IgnoreChildren(cl);
            ASTWalk.ModeVisitNodes(      &lt;* NOWARN *&gt;
                NARROW(cu.as_root, M3AST_AS.UNIT_NORMAL).as_block,
                SetPass1Closure(), ASTWalk.OnEntryAndExit);
          END;

    | M3AST_AS.Proc_decl(procDecl) =&gt;
          IF m = M3CScope.Change.Enter AND procDecl.as_body # NIL THEN
              ASTWalk.IgnoreChildren(cl);
              (* Resolve signature before entering procedure scope *)
            ASTWalk.ModeVisitNodes(procDecl.as_type,       &lt;* NOWARN *&gt;
                SetPass1Closure(), ASTWalk.OnEntryAndExit);
            (* Now enter procedure scope and resolve body *)
            M3CScope.Procedure(procDecl, m);
            ASTWalk.ModeVisitNodes(procDecl.as_body,       &lt;* NOWARN *&gt;
                SetPass1Closure(), ASTWalk.OnEntryAndExit);
          ELSE
            (* Covers 'Enter' if no body and both 'Exit' cases *)
            M3CScope.Procedure(procDecl, m);
          END;

    | M3AST_AS.Method(meth) =&gt;
        M3CScope.Method(meth, m);
    | M3AST_AS.Block(block) =&gt;
        M3CScope.Block(block, m);
    | M3AST_AS.For_st(for_st) =&gt;
        IF m = M3CScope.Change.Enter THEN For(cl, for_st) END;
    | M3AST_AS.Handler_id =&gt;
        IF m = M3CScope.Change.Enter THEN
          M3CScope.DefId(a, M3CScope.Change.Enter);
        END;
    | M3AST_AS.Handler(handler) =&gt;
          IF m = M3CScope.Change.Exit AND handler.as_id # NIL THEN
            M3CScope.DefId(handler.as_id, M3CScope.Change.Exit);
          END;

    | M3AST_AS.Tcase_id =&gt;
        IF m = M3CScope.Change.Enter THEN
          M3CScope.DefId(a, M3CScope.Change.Enter);
        END;
    | M3AST_AS.Tcase(tcase) =&gt;
          IF m = M3CScope.Change.Exit AND tcase.as_id # NIL THEN
            M3CScope.DefId(tcase.as_id, M3CScope.Change.Exit);
          END; (* if *)

    | M3AST_AS.Binding(binding) =&gt;
        IF m = M3CScope.Change.Enter THEN
            ASTWalk.IgnoreChildren(cl);
            (* Resolve expression being bound in outer scope *)
            ASTWalk.ModeVisitNodes(binding.as_exp,     &lt;* NOWARN *&gt;
                SetPass1Closure(), ASTWalk.OnEntryAndExit);
            (* Then add name being bound to scope *)
            M3CScope.DefId(binding.as_id, M3CScope.Change.Enter);
        END; (* if *)
    | M3AST_AS.With_st(with) =&gt;
        IF m = M3CScope.Change.Exit THEN
          VAR
            iterBindings := SeqM3AST_AS_Binding.NewIter(with.as_binding_s);
          BEGIN
            (* following proc pops the bindings in reverse order *)
            PopWithBindings(with.as_binding_s, iterBindings);
          END;
        END; (* if *)
    | M3AST_AS.From_import, M3AST_AS.Simple_import =&gt;
        (* should be resolved already *)
        IF m = M3CScope.Change.Enter THEN ASTWalk.IgnoreChildren(cl) END;

    | M3AST_AS.Qual_used_id(qualId) =&gt;
        IF m = M3CScope.Change.Enter THEN QualIdPass1(cl, qualId) END;

    | M3AST_AS.BINARY, M3AST_AS.Select,
      M3AST_AS.Call,
      M3AST_AS.Constructor,
      M3AST_AS.Used_interface_id, M3AST_AS.Used_def_id,
      M3AST_AS.Exp_used_id =&gt;
        IF m = M3CScope.Change.Enter THEN Resolve(cl, a, e); END;

    ELSE
      (* nothing *)
    END; (* case *)
  END SetPass1;

PROCEDURE <A NAME="Resolve"><procedure>Resolve</procedure></A>(
    cl: ASTWalk.Closure;
    a: AST.NODE;
    &lt;*UNUSED*&gt; vm: ASTWalk.VisitMode)
    RAISES {}=
  BEGIN
    TYPECASE a OF
    | M3AST_AS.Select(select) =&gt;
        SelectPass1(cl, select);
    | M3AST_AS.Call(call) =&gt;
        (* The keyword parameters can't be handled until the second pass *)
        CallPass1(cl, call);
    | M3AST_AS.Constructor(constructor) =&gt;
        (* The keyword elements can't be handled until the second pass *)
        ConstructorPass1(cl, constructor);
    | M3AST_AS.Qual_used_id(qualId) =&gt;
        QualIdPass1(cl, qualId);
    | M3AST_AS.Used_interface_id, M3AST_AS.Used_def_id =&gt;
        M3CScope.Lookup(a);
    | M3AST_AS.Exp_used_id(exp_used_id) =&gt;
        M3CScope.Lookup(exp_used_id.vUSED_ID);
    ELSE (* no action *)
    END;
  END Resolve;

PROCEDURE <A NAME="QualIdPass1"><procedure>QualIdPass1</procedure></A>(
    cl: ASTWalk.Closure;
    q: M3AST_AS.Qual_used_id)
    RAISES {}=
  VAR
    intfId := q.as_intf_id;
  BEGIN
    ASTWalk.IgnoreChildren(cl);
    IF intfId = NIL THEN
      M3CScope.Lookup(q.as_id);
    ELSE
      M3CScope.Lookup(intfId);
      ResolveInterfaceId(intfId.sm_def, q.as_id);
    END;
  END QualIdPass1;

PROCEDURE <A NAME="CallPass1"><procedure>CallPass1</procedure></A>(
    cl: ASTWalk.Closure;
    call: M3AST_AS.Call)
    RAISES {}=
  BEGIN
    ASTWalk.IgnoreChildren(cl);
    ASTWalk.VisitNodes(call.as_callexp,     &lt;* NOWARN *&gt;
                       NEW(ASTWalk.Closure, callback := Resolve).init());
    VAR
      iterActuals := SeqM3AST_AS_Actual.NewIter(call.as_param_s);
      actual: M3AST_AS.Actual;
    BEGIN
      WHILE SeqM3AST_AS_Actual.Next(iterActuals, actual) DO
        ASTWalk.VisitNodes(                 &lt;* NOWARN *&gt;
            actual.as_exp_type,
            NEW(ASTWalk.Closure, callback := Resolve).init());
      END; (* while *)
    END;
  END CallPass1;

PROCEDURE <A NAME="ConstructorPass1"><procedure>ConstructorPass1</procedure></A>(
    cl: ASTWalk.Closure;
    cons: M3AST_AS.Constructor)
    RAISES {}=
  VAR
    a: AST.NODE;
    iter: SeqM3AST_AS_CONS_ELEM.Iter;
    element: M3AST_AS.CONS_ELEM;
  BEGIN
    ASTWalk.IgnoreChildren(cl);
    ASTWalk.VisitNodes(cons.as_type,       &lt;* NOWARN *&gt;
                       NEW(ASTWalk.Closure, callback := Resolve).init());
    iter := SeqM3AST_AS_CONS_ELEM.NewIter(cons.as_element_s);
    WHILE SeqM3AST_AS_CONS_ELEM.Next(iter, element) DO
      TYPECASE element OF
      | M3AST_AS.Actual_elem(actualElem) =&gt;
          (* Avoid resolving keywords of any keyword actuals.. *)
          a := actualElem.as_actual.as_exp_type;
      ELSE
        a := element;
      END; (* if *)
      ASTWalk.VisitNodes(a, NEW(ASTWalk.Closure,      &lt;* NOWARN *&gt;
                                callback := Resolve).init());
    END; (* while *)
  END ConstructorPass1;

PROCEDURE <A NAME="SelectPass1"><procedure>SelectPass1</procedure></A>(
    cl: ASTWalk.Closure;
    select: M3AST_AS.Select)
    RAISES {}=
  BEGIN
    (* We cannot resolve the identifier to the right of the '.',
    since the search depends on the type of the left hand side, which
    is not known at this point (it depends on the resolution of the names
    in the left hand side).  So we truncate the walk and recursively resolve
    the left hand side. *)
    ASTWalk.IgnoreChildren(cl);
    ASTWalk.VisitNodes(select.as_exp,              &lt;* NOWARN *&gt;
                       NEW(ASTWalk.Closure, callback := Resolve).init());
  END SelectPass1;

PROCEDURE <A NAME="SelectPass2"><procedure>SelectPass2</procedure></A>(select: M3AST_AS.Select) RAISES {}=
  VAR
    exp_type, type_spec: M3AST_SM.TYPE_SPEC_UNSET;
    used_id: M3AST_AS.USED_ID;
    valid: BOOLEAN;
    a1: M3AST_AS.EXP;
  BEGIN
    (* Now we have to resolve the identifier on the right hand side of the
     '.' based on the type of the left. *)

    a1 := select.as_exp;
    exp_type :=
        M3CTypesMisc.Reveal(M3CTypesMisc.CheckedUnpack(a1.sm_exp_type_spec));

    (* If there has been an error already, leaving the exp type unset, then
     we don't bother with any further checking or resolving *)
    IF exp_type = NIL THEN RETURN END;

    used_id := select.as_id.vUSED_ID;

    (* If 'used_id' has a NIL name we have no hope of resolving it *)
    IF used_id.lx_symrep = NIL THEN RETURN END;

    valid := TRUE; (* we're optimistic to start with *)

    CASE M3CExpsMisc.Classify(a1) OF

    | M3CExpsMisc.Class.Interface =&gt;
        (* assert 'a1' must be an Exp_used_id, resolved to an interface id *)
        ResolveInterfaceId(NARROW(a1, M3AST_AS.Exp_used_id).vUSED_ID.sm_def, used_id);

    | M3CExpsMisc.Class.Type =&gt;
        TYPECASE exp_type OF
        | M3AST_AS.Enumeration_type(enumType) =&gt;
            M3CSearch.Member(enumType, used_id);
        | M3AST_AS.Object_type(objectType) =&gt;
            M3CSearch.FieldOrMethod(objectType, TRUE, used_id);
        ELSE
          valid := FALSE;
        END;

    | M3CExpsMisc.Class.Normal =&gt;
        TYPECASE exp_type OF
        | M3AST_AS.Record_type(record_type) =&gt;
            M3CSearch.Field(record_type, used_id);
        | M3AST_AS.Ref_type(ref_type) =&gt;
            M3CTypesMisc.GetTYPE_SPECFromM3TYPE(ref_type.as_type, type_spec);
            TYPECASE type_spec OF
            | NULL =&gt;
            | M3AST_AS.Record_type(recordType) =&gt;
                M3CSearch.Field(recordType, used_id);
            ELSE
              valid := FALSE;
            END;
        | M3AST_AS.Object_type(object_type) =&gt;
            M3CSearch.FieldOrMethod(object_type, FALSE, used_id);
        ELSE
          valid := FALSE;
        END;

    ELSE (* illegal, no action *)
        valid := FALSE;
    END; (* case *)

    IF NOT valid THEN
      M3Error.Report(a1,
          &quot;expression on the left of \'.\' cannot be selected&quot;);
    END;
  END SelectPass2;

PROCEDURE <A NAME="ResolveInterfaceId"><procedure>ResolveInterfaceId</procedure></A>(
    defId: M3AST_SM.DEF_ID_UNSET;
    usedId: M3AST_AS.USED_ID)
    RAISES {}=
  BEGIN
    TYPECASE defId OF
    | NULL =&gt;
    | M3AST_AS.Interface_id(intfId) =&gt;
        (* qualified name *)
        M3CSearch.Export(intfId.sm_spec, usedId);
    | M3AST_AS.Interface_AS_id(localId) =&gt;
        ResolveInterfaceId(localId.tmp_used_id.sm_def, usedId);
    ELSE
      M3Error.ReportWithId(usedId,
          &quot;identifer \'%s\' on left of \'.\' is not an interface&quot;,
          defId.lx_symrep);
    END;
  END ResolveInterfaceId;

PROCEDURE <A NAME="ResolveActualKeyword"><procedure>ResolveActualKeyword</procedure></A>(
    keyword: M3AST_AS.Exp_used_id;
    defId: M3AST_AS.DEF_ID)
    RAISES {}=
  BEGIN
    keyword.vUSED_ID.sm_def := defId;
  END ResolveActualKeyword;

BEGIN
END M3CDef.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
