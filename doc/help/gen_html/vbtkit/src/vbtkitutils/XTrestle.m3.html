<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: vbtkit/src/vbtkitutils/XTrestle.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>vbtkit/src/vbtkitutils/XTrestle.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation 
 All rights reserved. 
 See the file COPYRIGHT for a full description. 
 
 Last modified on Fri Jul 30 11:45:09 PDT 1993 by steveg 
      modified on Fri May 14 16:22:19 PDT 1993 by meehan 
      modified on Sun Dec 13 21:49:27 PST 1992 by mhb    
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="XTrestle.i3.html">XTrestle</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../../libm3/src/params/Params.i3.html">Params</A>, <A HREF="../../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../../ui/src/split/StableVBT.i3.html">StableVBT</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>,
       <A HREF="../../../ui/src/trestle/TrestleImpl.i3.html">TrestleImpl</A>, <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../../ui/src/vbt/VBTClass.i3.html">VBTClass</A>, <A HREF="XParam.i3.html">XParam</A>;

PROCEDURE <A NAME="Install"><procedure>Install</procedure></A> (v          : VBT.T;
                   applName   : TEXT    := NIL;
                   inst       : TEXT    := NIL;
                   windowTitle: TEXT    := NIL;
                   iconTitle  : TEXT    := NIL;
                   bgColorR   : REAL    := -1.0;
                   bgColorG   : REAL    := -1.0;
                   bgColorB   : REAL    := -1.0;
                   iconWindow : VBT.T   := NIL   )
  RAISES {TrestleComm.Failure, Error} =
  VAR
    display   : TEXT            := NIL;
    geometry  : TEXT            := NIL;
    screen    : INTEGER;
    g         : XParam.Geometry;
    trsl      : Trestle.T;
    needScreen                  := TRUE;
    i                           := 1;
  BEGIN
    TRY
      LOOP
        IF i &gt;= Params.Count - 1 THEN
          EXIT
        ELSIF Text.Equal(Params.Get(i), &quot;-display&quot;) THEN
          display := Params.Get(i + 1);
          screen := XParam.ParseDisplay(display).screen;
          needScreen := FALSE;
          INC(i, 2)
        ELSIF Text.Equal(Params.Get(i), &quot;-geometry&quot;) THEN
          geometry := Params.Get(i + 1);
          g := XParam.ParseGeometry(geometry);
          IF g.size = XParam.Missing THEN
            WITH shapes = VBTClass.GetShapes(v, FALSE) DO
              g.size.h := shapes[Axis.T.Hor].pref;
              g.size.v := shapes[Axis.T.Ver].pref;
            END
          END;
          INC(i, 2)
        ELSE
          INC(i)
        END
      END                        (* LOOP *)
    EXCEPT
      XParam.Error =&gt; RAISE Error
    END;

    trsl := Trestle.Connect(display);
    TrestleImpl.SetDefault(trsl);
    Trestle.Attach(v, trsl);
    Trestle.Decorate(v, inst, windowTitle, iconTitle, bgColorR, bgColorG,
                     bgColorB, applName, iconWindow);

    IF geometry = NIL THEN
      Trestle.MoveNear(v, NIL)
    ELSE
      StableVBT.SetShape(v, g.size.h, g.size.v);
      IF needScreen THEN
        screen := Trestle.ScreenOf(v, Point.Origin).id;
      END;
      WITH pos = XParam.Position(trsl, screen, g) DO
        Trestle.Overlap(v, screen, pos);
        (* XParam.Position can't always do the right thing if screen = NoScreen,
           so it does the best it can and then we check to see if that is right
           and do it again if it isn't.  NOTE: the second Trestle.Overlap
           call can be off by the height and width of the X border/title for
           the window *)
        IF screen = Trestle.NoScreen
             AND pos # XParam.Position(
                         trsl, Trestle.ScreenOf(v, Point.Origin).id, g) THEN
          Trestle.Overlap(
            v, screen,
            XParam.Position(trsl, Trestle.ScreenOf(v, Point.Origin).id, g));
        END;
      END;
    END;
  END Install;

BEGIN
END XTrestle.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
