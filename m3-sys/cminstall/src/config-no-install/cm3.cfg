%
% Unfortunately, command line defines are not currently
% available to the toplevel cm3.cfg.
%

%-------------------------------------------------------------------
% defined by cm3, but not the other MxConfig users
if not defined("CR") CR = "\n" end
if not defined("EOL") EOL = "\n" end
if not defined("M3_PROFILING") M3_PROFILING = FALSE end
if not defined("SL") SL = "/" end
%-------------------------------------------------------------------

proc CM3SetInstallRoot() is
    if defined("INSTALL_ROOT")
        return
    end
    if not equal ($CM3_INSTALL, "")
        INSTALL_ROOT = $CM3_INSTALL
        return
    end
    INSTALL_ROOT = (path() & SL & "..")
end

%
% This file can be split up.
% If we know the source root, we can push code into it.
%

CM3SetInstallRoot()

if not defined("ROOT")
    if not equal($CM3_ROOT, "")
        ROOT = $CM3_ROOT
        %include(ROOT & SL & "m3-sys" & SL & "cminstall" & SL & "src" & SL & "config" & SL & "cm3.cfg")
        %return
    end
end

local readonly proc CM3TargetProbe() is

    if not equal($CM3_TARGET, "")
        TARGET = $CM3_TARGET
        return
    end

    if defined("TARGET")
        return
    end

    if defined("HOST")
        TARGET = HOST
        write("defaulting to native build: ", TARGET, EOL)
        return
    end

    if equal($OS, "Windows_NT")
        TARGET = "I386_NT"
        write("defaulting to native build: ", TARGET, EOL)
        return
    end

    error("unable to determine target -- try setting the CM3_TARGET environment variable" & CR
          & "Possible values include but are not limited to:" & CR
          & "  I386_NT, I386_FREEBSD, I386_DARWIN, I386_LINUX, I386_NETBSD," & CR
          & "  AMD64_FREEBSD, AMD64_DARWIN, AMD64_LINUX," & CR
          & "  PPC_DARWIN, PPC_LINUX, SPARC32_SOLARIS," & CR
          & "  SPARC32_SOLARIS, SPARC64_SOLARIS, I386_SOLARIS, AMD64_SOLARIS," & CR
          & "  SPARC32_LINUX" & CR)
end

CM3TargetProbe()

local readonly proc CM3ConfigInclude(a) is

    local readonly proc FileExists(a) is
      return not stale (a, a)
    end

    %write("checking " & a & CR)
    if FileExists(a)
        %write("using " & a & CR)
        return a
    end
    return FALSE
end

local readonly proc CM3ConfigProbe() is

    %
    % In order for this file to be useful in a distribution without source, first look near the compiler.
    %
    local p = path()
    local a = ""

    foreach a in [(p & SL & "config" & SL & TARGET),
                  (p & SL & ".." & SL & TARGET & ".config"),
                  (p & SL & TARGET & ".cfg"),
                  (p & SL & TARGET),
                  (p & SL & "cm3.exe.config"),
                  (p & SL & "cm3.config")]
        if CM3ConfigInclude(a)
            return a
        end
    end

    %
    % Then go to the source tree.
    %
    local readonly proc CM3SetSourceRoot() is
        if defined("ROOT")
            return
        end
        if not equal($CM3_ROOT, "")
            ROOT = $CM3_ROOT
            return
        end
        error("Neither the variable ROOT nor environment variables ROOT or CM3_ROOT is defined.")
    end

    CM3SetSourceRoot()

    Result = CM3ConfigInclude(ROOT & SL & "m3-sys" & SL & "cminstall" & SL & "src" & SL & "config-no-install" & SL & TARGET)
    if Result
        return Result
    end

    %Result = CM3ConfigInclude(ROOT & SL & "m3-sys" & SL & "cminstall" & SL & "src" & SL & "config" & SL & TARGET)
    %if Result
    %    return Result
    %end

    %
    % Give up.
    %
    error("unable to find a configuration file for (" & INSTALL_ROOT & ", " & ROOT & ", " & TARGET & ")")
end

include(CM3ConfigProbe())
